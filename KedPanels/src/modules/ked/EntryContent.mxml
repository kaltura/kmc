<?xml version="1.0" encoding="utf-8"?>
<mx:Module layout="vertical" xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" styleName="contentVbox"
		   implements="com.kaltura.edw.business.IDrilldownPanel"
		   xmlns:controls="com.kaltura.controls.*" 
		   xmlns:view="com.kaltura.edw.view.*" xmlns:et="com.kaltura.edw.components.et.*" xmlns:control="com.kaltura.edw.control.*"
		   label="{resourceManager.getString('drilldown', 'mixesTab')}">
	<mx:Metadata>
		[Event(name="validationComplete", type="com.kaltura.edw.events.ValidationResultEvent")] 
		[Event(name="saved", type="com.kaltura.edw.events.InternalKedEvent")] 
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import com.kaltura.analytics.GoogleAnalyticsConsts;
			import com.kaltura.analytics.GoogleAnalyticsTracker;
			import com.kaltura.analytics.KAnalyticsTracker;
			import com.kaltura.analytics.KAnalyticsTrackerConsts;
			import com.kaltura.edw.business.permissions.PermissionManager;
			import com.kaltura.edw.constants.PanelConsts;
			import com.kaltura.edw.control.events.KedEntryEvent;
			import com.kaltura.edw.events.InternalKedEvent;
			import com.kaltura.edw.events.ValidationResultEvent;
			import com.kaltura.edw.model.datapacks.ContentDataPack;
			import com.kaltura.edw.model.datapacks.EntryDataPack;
			import com.kaltura.kmvc.model.IDataPackRepository;
			import com.kaltura.kmvc.model.KMvCModel;
			import com.kaltura.types.KalturaStatsKmcEventType;
			import com.kaltura.vo.KalturaBaseEntry;
			import com.kaltura.vo.KalturaLiveStreamAdminEntry;
			import com.kaltura.vo.KalturaMixEntry;
			
			import mx.binding.utils.BindingUtils;
			import mx.binding.utils.ChangeWatcher;
			
			[Bindable]
			/**
			 * @copy #selectedEntry
			 * */
			private var _selectedEntry:KalturaBaseEntry;
			
			private var _model:ContentDataPack;
			
			/**
			 * to watch when <code>selectedEntry.parts</code> change
			 * */
			private var _cw:ChangeWatcher;
			
			/**
			 * whether we should load roughcuts
			 * */
			public var loadRoughcuts:Boolean = true;
			
			private var _dpModel:IDataPackRepository;
			
			public function get selectedEntry():KalturaBaseEntry {
				return _selectedEntry;
			}
			
			
			public function set selectedEntry(value:KalturaBaseEntry):void {
				_selectedEntry = value;
				
				var resourceName:String = (_selectedEntry is KalturaMixEntry) ? 'content' : 'mixesTab';
				label = resourceManager.getString('drilldown', resourceName);
			}
			
			public function get dataPackModel():IDataPackRepository{
				return _dpModel;
			}
			public function set dataPackModel(value:IDataPackRepository):void{
				_dpModel = value;
				var edp:EntryDataPack = _dpModel.getDataPack(EntryDataPack) as EntryDataPack;
				loadRoughcuts = edp.loadRoughcuts;
			}
			
			public function get helpAnchor():String{
				return "entry_content";
			}
			
			
			public function validate():void {
				var e:ValidationResultEvent = new ValidationResultEvent(ValidationResultEvent.VALIDATION_COMPLETE, true);
				dispatchEvent(e);
			}
			
			
			public function save():void {
				dispatchEvent(new InternalKedEvent(InternalKedEvent.SAVED));
			}
			
			
			public function clear():void {
				var e:KedEntryEvent = new KedEntryEvent(KedEntryEvent.RESET_PARTS, null);
				controller.dispatch(e);
			}
			
			
			public function isChanged():Boolean {
				return false;
			}
			
			
			public function initData():void {
				// get data
				loadEntries();
			}
			
			
			public function init():void {
				// monitor data change if needed
				if (!_cw) {
					var e:KedEntryEvent = new KedEntryEvent(KedEntryEvent.RESET_PARTS, null);
					controller.dispatch(e);
					_model = KMvCModel.getInstance().getDataPack(ContentDataPack) as ContentDataPack;
					_cw = BindingUtils.bindSetter(removeTab, _model, "contentParts");
				}
				
				PermissionManager.getInstance().applyAllAttributes(this, PanelConsts.CONTENT_PANEL);
			}
			
			public function destroy():void {
				_cw.unwatch();
			}
			
			private function onPaging():void {
				loadEntries();
				GoogleAnalyticsTracker.getInstance().sendToGA(GoogleAnalyticsConsts.GO_TO_PAGE, GoogleAnalyticsConsts.CONTENT);
				KAnalyticsTracker.getInstance().sendEvent(KAnalyticsTrackerConsts.CONTENT, KalturaStatsKmcEventType.CONTENT_CONTENT_GO_TO_PAGE, "content>Show Rows");
			}
			
			/**
			 * load all mix entries or load mixes that uses this entry if it is not a mix
			 */
			private function loadEntries():void {
				if (_selectedEntry is KalturaMixEntry) {
					//get all mix entries
					var cgEvent:KedEntryEvent = new KedEntryEvent(KedEntryEvent.GET_ALL_ENTRIES, _selectedEntry);
					controller.dispatch(cgEvent);
				} 
				else if (loadRoughcuts && !(_selectedEntry is KalturaLiveStreamAdminEntry)) {
					//get all entries that uses this mix 
					var getRoughcuts:KedEntryEvent = new KedEntryEvent(KedEntryEvent.GET_ENTRY_ROUGHCUTS, _selectedEntry);
					controller.dispatch(getRoughcuts);
				}
				
			}
			
			/**
			 * remove column that are not relevant for
			 * this view from the entry contents table
			 * */
			private function removeColums(event:Event):void {
				event.target.removeColumn(resourceManager.getString('drilldown', 'preview'));
				event.target.removeColumn(resourceManager.getString('drilldown', 'idHeader'));
				event.target.removeColumn(resourceManager.getString('drilldown', 'mediaType'));
				event.target.removeColumn(resourceManager.getString('drilldown', 'playlistType'));
				event.target.removeColumn(resourceManager.getString('drilldown', 'flags'));
				event.target.removeColumn(resourceManager.getString('drilldown', 'moderationStatus'));
				event.target.removeColumn(resourceManager.getString('drilldown', 'creator'));
				event.target.removeColumn(resourceManager.getString('drilldown', 'rating'));
				event.target.removeColumn(resourceManager.getString('drilldown', 'playerLoads'));
				event.target.removeColumn(resourceManager.getString('drilldown', 'intime'));
			}
			
			private function removeTab(value:Array):void {
				if (value) {
					if (value.length == 0 && this.parent) {
						this.parent.removeChild(this);
						return;
					}
				}
				table.dataProvider = value;
			}
		]]>
	</mx:Script>
	<control:ContentTabController id="controller" />
	<et:EntryTable id="table" width="100%" height="100%" creationComplete="{removeColums(event)}"
				   sortableColumns="false" showLinks="false"/>
	<controls:Paging id="paging" width="100%" nextPage="onPaging()" prvPage="onPaging()" 
					 getPageNum="onPaging()" showRowsInPage="false"/>
</mx:Module>
