<?xml version="1.0" encoding="utf-8"?>
<!---
	 a menu that allows adding content
-->
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" width="208" layout="vertical" styleName="addPanel"
		   preinitialize="createShowingHandler(event)" creationComplete="creationCompleteHandler(event)"
		   implements="com.kaltura.kmc.business.IKmcPlugin,com.kaltura.kmc.business.IPopupMenu" visible="false">
	<mx:Metadata>
		[ResourceBundle("create")]
	</mx:Metadata>
			
	<mx:Script>
		<![CDATA[
			import com.kaltura.KalturaClient;
			import com.kaltura.commands.conversionProfile.ConversionProfileList;
			import com.kaltura.events.KalturaEvent;
			import com.kaltura.kmc.business.IKmcPlugin;
			import com.kaltura.kmc.business.IPopupMenu;
			import com.kaltura.kmc.business.JSGate;
			import com.kaltura.kmc.business.MenuHandler;
			import com.kaltura.kmc.business.PermissionManager;
			import com.kaltura.kmc.events.KmcNavigationEvent;
			import com.kaltura.kmc.modules.create.BulkUploader;
			import com.kaltura.kmc.modules.create.MultipleMediaFilesSelector;
			import com.kaltura.kmc.modules.create.view.FileUploadSettings;
			
			import mx.controls.Alert;
			import mx.core.Application;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;


			/**
			 * url of samples file
			 * @internal 
			 * passed in from uiconf plugin node by KmcPluginManager
			 * */
			public var samplesUrl:String;
			
			private const BTN_WIDTH:int = 150;
			
			/*
				WINDOW STATES
			*/
			private const MENU_STATE:String = "menu_state";
			private const WEBCAM_STATE:String = "webcam_state";
			private const SERVICES_STATE:String = "services_state";

			private var _showingHandler:MenuHandler;

			private var _flashvars:Object;

			private var _state:String = MENU_STATE;

			/**
			 * application root for the menu handler
			 * */
			private var _approot:DisplayObjectContainer;

			/**
			 * @copy #client
			 * */
			private var _kc:KalturaClient;

			protected function creationCompleteHandler(event:FlexEvent):void {
				PermissionManager.getInstance().applyAllAttributes(this, "addPanel");
				menuScreen.invalidateSize();
				validateSize(true);
				
				viewStack.height = menuScreen.measuredHeight;
				this.height = viewStack.height + getStyle("paddingTop") + getStyle("paddingBottom"); 
				visible = true;
			}

			protected function showUploadSettingsWindow(e:Event):void {
				e.target.removeEventListener(MultipleMediaFilesSelector.FILES_SELECTED, showUploadSettingsWindow);
				var fus:FileUploadSettings = new FileUploadSettings();
				fus.client = _kc;
				fus.uploadFiles((e.target as MultipleMediaFilesSelector).getFiles());
				PopUpManager.addPopUp(fus, (Application.application as DisplayObject), true);
				PopUpManager.centerPopUp(fus);
			}


			/**
			 * Opens a desktop file selection pop-up. Allowing video, audio and image files selection
			 * */
			public function addUpload(event:MouseEvent = null):void {
				var neu:MultipleMediaFilesSelector = new MultipleMediaFilesSelector();
				neu.addEventListener(MultipleMediaFilesSelector.FILES_SELECTED, showUploadSettingsWindow);
				neu.doFileUpload();
			}




			/**
			 * opens a KCW with only webcam recording option
			 * */
			protected function btnWebcam_clickHandler(event:MouseEvent):void {
				showOptionsScreen(resourceManager.getString('create', 'webcam_state_title'));
			}


			/**
			 * Opens a desktop file selection pop-up, allowing csv/xml files selection
			 * */
			public function addBulk(event:MouseEvent = null):void {
				var bu:BulkUploader = new BulkUploader(_kc);
				bu.doUpload();
			}


			/**
			 * Opens a KCW with audio/video/image online services options only
			 * */
			protected function btnServices_clickHandler(event:MouseEvent):void {
				showOptionsScreen(resourceManager.getString('create', 'services_state_title'));
			}
			
			


			/**
			 * downloads csv and xml samples
			 * */
			protected function btnSamples_clickHandler(event:MouseEvent):void {
				var req:URLRequest = new URLRequest(samplesUrl);
				navigateToURL(req);
			}


			/**
			 * Opens a video entry drill down in a "new entry" mode on top
			 * of content management page
			 * */
			protected function btnVideo_clickHandler(event:MouseEvent):void {
				dispatchEvent(new KmcNavigationEvent(KmcNavigationEvent.NAVIGATE, "content", "manage", {kmcFunction: "content.createVideoEntry"}, true));
				hidePanel();
			}


			/**
			 * Opens an audio entry drill down in a "new entry" mode on top
			 * of content management page
			 * */
			protected function btnAudio_clickHandler(event:MouseEvent):void {
				dispatchEvent(new KmcNavigationEvent(KmcNavigationEvent.NAVIGATE, "content", "manage", {kmcFunction: "content.createAudioEntry"}, true));
				hidePanel();
			}


			/**
			 * Opens the new live stream window on top of content management page
			 * */
			protected function btnLive_clickHandler(event:MouseEvent):void {
				dispatchEvent(new KmcNavigationEvent(KmcNavigationEvent.NAVIGATE, "content", "manage", {kmcFunction: "content.createLiveEntry"}, true));
				hidePanel();
			}
			
			
			/**
			 * shows the options screen
			 * @param title	text to show at the top of the screen
			 * */
			protected function showOptionsScreen(title:String):void {
				_state = WEBCAM_STATE;
				viewStack.selectedChild = optionsScreen;
				lblState.text = title;
				// get conversion profiles list
				if (!cbConvProfiles.dataProvider.length) {
					enabled = false;
					var cpl:ConversionProfileList = new ConversionProfileList();
					cpl.addEventListener(KalturaEvent.COMPLETE, onConvProfListed);
					cpl.addEventListener(KalturaEvent.FAILED, onConvProfListed);
					_kc.post(cpl);
				}
			}
			
			protected function onConvProfListed(e:KalturaEvent):void {
				enabled = true;
				if (e.type == KalturaEvent.FAILED) {
					Alert.show(e.error.errorMsg, resourceManager.getString('create', 'error'));
				}
				else {
					cbConvProfiles.dataProvider = e.data.objects;
				}
			}

			protected function cancel_clickHandler(event:MouseEvent):void {
				resetPanel();
			}
			
			
			/**
			 * open KCW with desired config
			 * */
			protected function openKcw(event:MouseEvent):void {
				switch (_state) {
					case WEBCAM_STATE: 
						JSGate.openKcw(cbConvProfiles.selectedItem.id, "uploadWebCam");
						break;
					case SERVICES_STATE:
						JSGate.openKcw(cbConvProfiles.selectedItem.id, "uploadImport");
						break;
				}
			}
			
			protected function resetPanel(e:Event = null):void {
				_state = MENU_STATE;
				viewStack.selectedChild = menuScreen;
			}


			protected function createShowingHandler(event:FlexEvent):void {
				_showingHandler = new MenuHandler(this, "positionAddPanel");
				_showingHandler.addEventListener(MenuHandler.MENU_REMOVED, resetPanel);
				if (_approot) {
					_showingHandler.setRoot(_approot);
				}
			}


			public function setRoot(doc:DisplayObjectContainer):void {
				_approot = doc;
				if (_showingHandler) {
					_showingHandler.setRoot(doc);
				}
			}


			/**
			 * show the panel
			 * */
			public function showPanel():void {
				_showingHandler.showPanel();
			}


			/**
			 * hide the panel
			 */
			public function hidePanel(me:MouseEvent = null):void {
				_showingHandler.hidePanel(me);
				resetPanel();
			}


			/**
			 * position the panel
			 * @param right
			 */
			public function positionPanel(right:Number):void {
				_showingHandler.positionPanel(right);
			}
			

			/**
			 * KalturaClient used for API calls
			 * */
			public function get client():KalturaClient {
				return _kc;
			}


			public function set client(value:KalturaClient):void {
				_kc = value;
			}


			public function get flashvars():Object {
				return _flashvars;
			}


			public function set flashvars(value:Object):void {
				_flashvars = value;
			}
			

			

		]]>
	</mx:Script>
	<mx:ViewStack id="viewStack" creationPolicy="all" 
				  height="{menuScreen.measuredHeight}" width="100%" styleName="noPadding" >
		<mx:VBox styleName="noPadding" id="menuScreen" width="100%">
			<mx:Label styleName="addPanelLabel" htmlText="{resourceManager.getString('create', 'upload_lbl')}"
					  includeInLayout="{uploadBox.includeInLayout}" visible="{uploadBox.visible}"/>
			<mx:VBox id="uploadBox" styleName="addPanelButtonBox" width="100%" horizontalAlign="center"
					 visible="{btnUpload.visible || btnWebcam.visible}"
					 includeInLayout="{btnUpload.includeInLayout || btnWebcam.includeInLayout}">
				<mx:Button id="btnUpload" click="addUpload(event)" width="{BTN_WIDTH}" styleName="addPanelButton"
						   label="{resourceManager.getString('create', 'upload_btn')}"/>
				<mx:Button id="btnWebcam" click="btnWebcam_clickHandler(event)" width="{BTN_WIDTH}" styleName="addPanelButton"
						   label="{resourceManager.getString('create', 'webcam_btn')}"/>
			</mx:VBox>
			<mx:Label styleName="addPanelLabel" htmlText="{resourceManager.getString('create', 'import_lbl')}"
					  includeInLayout="{importBox.includeInLayout}" visible="{importBox.visible}"/>
			<mx:VBox id="importBox" styleName="addPanelButtonBox" width="100%" horizontalAlign="center"
					 visible="{btnBulk.visible || btnServices.visible}"
					 includeInLayout="{btnBulk.includeInLayout || btnServices.includeInLayout}">
				<mx:Button id="btnBulk" click="addBulk(event)" width="{BTN_WIDTH}" styleName="addPanelButton"
						   label="{resourceManager.getString('create', 'bulk_btn')}"/>
				<mx:Button id="btnServices" click="btnServices_clickHandler(event)" width="{BTN_WIDTH}"
						   styleName="addPanelButton" label="{resourceManager.getString('create', 'services_btn')}"/>
				<mx:LinkButton id="btnSamples" click="btnSamples_clickHandler(event)" styleName="addPanelLinkButton"
							   label="{resourceManager.getString('create', 'samples_btn')}"
							   visible="{btnBulk.visible}" includeInLayout="{btnBulk.includeInLayout}"/>
			</mx:VBox>
			<mx:Label styleName="addPanelLabel" htmlText="{resourceManager.getString('create', 'prepare_lbl')}"
					  includeInLayout="{prepareBox.includeInLayout}" visible="{prepareBox.visible}"/>
			<mx:VBox id="prepareBox" styleName="addPanelButtonBox" width="100%" horizontalAlign="center"
					 visible="{btnVideo.visible || btnAudio.visible || btnLive.visible}"
					 includeInLayout="{btnVideo.includeInLayout || btnAudio.includeInLayout || btnLive.includeInLayout}">
				<mx:Button id="btnVideo" click="btnVideo_clickHandler(event)" width="{BTN_WIDTH}" styleName="addPanelPrepButton"
						   label="{resourceManager.getString('create', 'video_btn')}"/>
				<mx:Button id="btnAudio" click="btnAudio_clickHandler(event)" width="{BTN_WIDTH}" styleName="addPanelPrepButton"
						   label="{resourceManager.getString('create', 'audio_btn')}"/>
				<mx:Button id="btnLive" click="btnLive_clickHandler(event)" width="{BTN_WIDTH}" styleName="addPanelPrepButton"
						   label="{resourceManager.getString('create', 'live_btn')}"/>
			</mx:VBox>
		</mx:VBox>
		
		<mx:VBox id="optionsScreen" styleName="noPadding" height="{menuScreen.height}" width="100%">
			<mx:Label id="lblState" styleName="addPanelLabel" 
					  htmlText="{resourceManager.getString('create', 'webcam_state_title')}" />
			<mx:VBox styleName="addPanelExtraBg" >
				<mx:Label htmlText="{resourceManager.getString('create', 'select_profile')}" />
				<mx:ComboBox id="cbConvProfiles" labelField="name"/>
				<mx:Spacer height="100%" />
				<mx:HBox>
					<mx:Button click="openKcw(event)" styleName="addPanelNextBtn"  
							   label="{resourceManager.getString('create', 'next_label')}" />
					<mx:LinkButton click="cancel_clickHandler(event)" styleName="addPanelCancelBtn"
								   label="{resourceManager.getString('create', 'cancel_label')}" />
				</mx:HBox>
			</mx:VBox>
		</mx:VBox>
	</mx:ViewStack>
</mx:Module>
