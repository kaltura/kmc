<?xml version="1.0" encoding="utf-8"?>
<!---
 	this component handles all bulk submissions in upload module
-->
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" horizontalAlign="center" styleName="addPanelButtonBox" xmlns:controls="com.kaltura.controls.*" >
	<mx:Metadata>
		/**
		 * request to hide the panel
		 * */
		[Event(name="hidePanel",type="flash.events.Event")]
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import com.kaltura.KalturaClient;
			import com.kaltura.kmc.events.KmcNavigationEvent;
			import com.kaltura.kmc.modules.create.BulkUploader;
			import com.kaltura.kmc.modules.create.types.BulkTypes;
			
			import modules.AddEntryPanel;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.core.mx_internal;
			import mx.events.CloseEvent;
			import mx.events.ListEvent;
			import mx.managers.PopUpManager;
			
			/**
			 * KalturaClient for API calls
			 * */
			public var client:KalturaClient;

			[Bindable]
			/**
			 * URL from which samples file can be loaded
			 * */
			public var samplesUrl:String;
			
			
			[Bindable]
			private var _bulkTypes:ArrayCollection = new ArrayCollection([BulkTypes.MEDIA, BulkTypes.CATEGORY, BulkTypes.USER, BulkTypes.CATEGORY_USER]);
			
			
			private var _submissionAlert:Alert;
			
			/**
			 * RnP: show media bulk uplooad button
			 * */
			public function set allowMedia(value:Boolean):void {
				removeType(BulkTypes.MEDIA);
			}
			
			public function get allowMedia():Boolean {
				return true;
			}
			
			/**
			 * RnP: show categories bulk uplooad button
			 * */
			public function set allowCategories(value:Boolean):void {
				removeType(BulkTypes.CATEGORY);
			}
			
			public function get allowCategories():Boolean {
				return true;
			}
			
			/**
			 * RnP: show users bulk uplooad button
			 * */
			public function set allowUsers(value:Boolean):void {
				removeType(BulkTypes.USER);
			}
			
			public function get allowUsers():Boolean {
				return true;
			}
			
			/**
			 * RnP: show members (category users) bulk uplooad button
			 * */
			public function set allowMembers(value:Boolean):void {
				removeType(BulkTypes.CATEGORY_USER);
			}
			
			public function get allowMembers():Boolean {
				return true;
			}
			
			private function removeType (type:String):void {
				for (var i:int = 0; i<_bulkTypes.length; i++) {
					if (_bulkTypes.getItemAt(i) == type) {
						_bulkTypes.removeItemAt(i);
						break;
					}
				}
				// if all buttons are gone, remove the whole panel
				if (_bulkTypes.length == 0) {
					visible = includeInLayout = false;
				}
			}
			
			
			
			/**
			 * get label corresponding to item text for dropdown
			 */
			private function getLabel(item:Object):String {
				var res:String = item.toString();
				switch (item.toString()) {
					case BulkTypes.MEDIA:
						res = resourceManager.getString('create', 'bulk_entries');
						break;
					case BulkTypes.CATEGORY:
						res = resourceManager.getString('create', 'bulk_cats');
						break;
					case BulkTypes.USER:
						res = resourceManager.getString('create', 'bulk_users');
						break;
					case BulkTypes.CATEGORY_USER:
						res = resourceManager.getString('create', 'bulk_cat_users');
						break;
				}
				return res;
			}
			
			
			/**
			 * tell content page to refresh bulk upload log
			 * */
			private function onBulkAdded(e:Event):void {
				var string:String = resourceManager.getString('create', 'bulk_submitted');
				_submissionAlert = Alert.show(string, '', Alert.OK, null, refreshOrClose);
				_submissionAlert.mx_internal::alertForm.mx_internal::textField.htmlText = string;
				_submissionAlert.mx_internal::alertForm.mx_internal::textField.htmlText = string;
				_submissionAlert.mx_internal::alertForm.mx_internal::textField.addEventListener("link", gotoBulkLog);
			}
			
			
			private function gotoBulkLog(evet:Event = null):void {
				// go to log
				dispatchEvent(new KmcNavigationEvent(KmcNavigationEvent.NAVIGATE, "content", "bulk", 
					null, true));
				// refresh log 
				dispatchEvent(new KmcNavigationEvent(KmcNavigationEvent.NAVIGATE, "content", "bulk", 
					{kmcFunction: "content.refreshBulk"}, true));
				// remove alert
				PopUpManager.removePopUp(_submissionAlert);
			}
			
			
			private function refreshOrClose(event:CloseEvent):void {
				// refresh log
				dispatchEvent(new KmcNavigationEvent(KmcNavigationEvent.NAVIGATE, "content", "manage", 
					{kmcFunction: "content.refreshBulk"}, true));
			}
			
			
			/**
			 * downloads csv and xml samples
			 * */
			public function downloadSampleFiles(event:MouseEvent = null):void {
				var req:URLRequest = new URLRequest(getFullUrl(samplesUrl));
				navigateToURL(req);
				dispatchEvent(new Event("hidePanel"));
			}
			
			
			private function getFullUrl(url:String):String {
				if (url.indexOf('://') == -1) {
					// no protocol, concat from client
					url = client.protocol + client.domain + url; 
				}
				return url;
			}
			
			
			/**
			 * Opens a desktop file selection pop-up, allowing csv/xml files selection
			 * @param type tyoe of object for bulk upload
			 * 
			 * @see com.kaltura.kmc.modules.create.types.BulkTypes 
			 * */
			public function addBulk(type:String):void {
				dispatchEvent(new Event("hidePanel"));
				var bu:BulkUploader = new BulkUploader(client);
				bu.addEventListener(Event.COMPLETE, onBulkAdded);
				bu.doUpload(type);
			}
			


			private function dropdown_changeHandler(event:ListEvent):void {
				addBulk(dropdown.selectedItem.toString());					
				dropdown.selectedIndex = -1;
			}

			
		]]>
	</mx:Script>
	
	<mx:ComboBox id="dropdown" styleName="actionsComboBox" selectedIndex="-1" width="{AddEntryPanel.BTN_WIDTH}"
				 prompt="{resourceManager.getString('create', 'bulk_btn')}"
				 change="dropdown_changeHandler(event)" dataProvider="{_bulkTypes}" labelFunction="getLabel"/>
	
	<controls:MultilineLinkButton id="btnSamples" click="downloadSampleFiles(event)" styleName="addPanelLinkButton"
				   label="{resourceManager.getString('create', 'samples_btn')}" width="{AddEntryPanel.BTN_WIDTH}" />
</mx:VBox>
