<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%"
		 xmlns:view="com.kaltura.kmc.modules.admin.view.*" xmlns:controls="com.kaltura.controls.*"
		 implements="com.kaltura.kmc.modules.admin.view.IAdminSubtab"
		 creationComplete="creationCompleteHandler()">
	<mx:Script>
		<![CDATA[
			import com.adobe.cairngorm.control.CairngormEvent;
			import com.kaltura.kmc.business.PermissionManager;
			import com.kaltura.kmc.events.KmcErrorEvent;
			import com.kaltura.kmc.events.KmcHelpEvent;
			import com.kaltura.kmc.modules.admin.control.events.DrilldownEvent;
			import com.kaltura.kmc.modules.admin.control.events.ListItemsEvent;
			import com.kaltura.kmc.modules.admin.control.events.RoleEvent;
			import com.kaltura.kmc.modules.admin.control.events.UserEvent;
			import com.kaltura.kmc.modules.admin.model.DrilldownMode;
			import com.kaltura.kmc.modules.admin.model.RolesModel;
			import com.kaltura.kmc.modules.admin.model.UsersModel;
			import com.kaltura.types.KalturaUserStatus;
			import com.kaltura.vo.KalturaFilterPager;
			import com.kaltura.vo.KalturaUser;
			import com.kaltura.vo.KalturaUserRole;
			
			import mx.binding.utils.BindingUtils;
			import mx.collections.ArrayCollection;
			import mx.managers.PopUpManager;
			import mx.resources.ResourceManager;

			[Bindable]
			/**
			 * users related part of the main model
			 * */
			public var model:UsersModel;
			
			[Bindable]
			/**
			 * roles related part of the main model
			 * */
			public var rolesModel:RolesModel;


			
			
			/**
			 * a popup window with user details
			 * */
			protected var _userDrilldown:UserDrilldown;

			
			/**
			 * a popup window with user details
			 * */
			protected var _roleDrilldown:RoleDrilldown;
			


			
			/**
			 * refresh subtab data
			 * */
			public function refreshData():void {
				listRoles();
				listUsers(0);
				// reset selected user
				var ce:UserEvent;
				ce = new UserEvent(UserEvent.SELECT_USER, null);
				ce.dispatch();
				
				var cg:CairngormEvent = new ListItemsEvent(ListItemsEvent.LIST_PARTNER_PERMISSIONS);
				cg.dispatch();
			}
			
			
			/**
			 * get all roles associated with the current partner
			 * */
			protected function listRoles():void {
				var ce:ListItemsEvent;
				ce = new ListItemsEvent(ListItemsEvent.LIST_ROLES);
				ce.dispatch();
			}
			
			 
			/**
			 * bind setters and apply permissions 
			 * */
			protected function creationCompleteHandler():void {
				BindingUtils.bindSetter(onDrilldownStateChange, model, "drilldownMode");
				BindingUtils.bindSetter(onRoleDrilldownStateChange, model, "roleDrilldownMode");
				BindingUtils.bindSetter(onRolesListChange, rolesModel, "roles");
				BindingUtils.bindSetter(onNewRoleChange, model, "newRole");
				BindingUtils.bindSetter(onUsersQuotaOrTotalChange, model, "loginUsersQuota");
				BindingUtils.bindSetter(onUsersQuotaOrTotalChange, model, "totalUsers");
				PermissionManager.getInstance().applyAllAttributes(this,id);
				
				// get users quota
			}
			
			
			/**
			 * update the quota label according to model values 
			 * */
			protected function onUsersQuotaOrTotalChange(value:int):void {
				// don't use the param value because we don't know whether it's quota or total
				var user_s:String;
				if (model.totalUsers == 1) {
					// singular
					user_s = ResourceManager.getInstance().getString('admin', 'users_quota_user');
				} 
				else {
					// plural
					user_s = ResourceManager.getInstance().getString('admin', 'users_quota_users');
				}
				var left:int = (model.loginUsersQuota - model.totalUsers);
				// users_quota = {0} KMC {1} already in use. {2} still available. 
				var s:String = ResourceManager.getInstance().getString('admin', 'users_quota', [model.totalUsers, user_s, left.toString()]);
				usersQuota.text = s;
				usersQuotaBtn.enabled = left > 0;
			}
			
			
			/**
			 * if a user drilldown window is opened, update the user's role. 
			 * */
			protected function onNewRoleChange(value:KalturaUserRole):void {
				if (value) {
					if (_userDrilldown) {
						_userDrilldown.setUserRole(value.id);
					}
				}
			}
			
			/**
			 * if a user drilldown window is opened, update its roles list.
			 * */
			protected function onRolesListChange(value:ArrayCollection):void {
				if (_userDrilldown) {
					_userDrilldown.roles = value;
				}
			}
			
			/**
			 * re-load data, close popup, etc.
			 * */
			protected function onDrilldownStateChange(value:String):void {
				switch (value) {
					case DrilldownMode.ADD:
					case DrilldownMode.EDIT:
						// do nothing
						break;
					case DrilldownMode.NONE:
						// if we got here, data save completed successfuly
						closeUserDrilldown(null);
						refreshData();
						break;
				}
			}
			
			
			/**
			 * close popup.
			 * */
			protected function onRoleDrilldownStateChange(value:String):void {
				switch (value) {
					case DrilldownMode.ADD:
					case DrilldownMode.EDIT:
						// do nothing
						break;
					case DrilldownMode.NONE:
						// if we got here, data save completed successfuly
						closeRoleDrilldown(null);
						refreshData();
						break;
				}
			}
			
			
			/**
			 * dispatch Cairngorm event to raise a list users command 
			 * */
			protected function listUsers(pageNumber:int):void {
				var kfp:KalturaFilterPager = new KalturaFilterPager();
				if (paging.kalturaFilterPager.pageSize != int.MIN_VALUE) {
					kfp.pageSize = paging.kalturaFilterPager.pageSize;
				}
				else {
					kfp.pageSize = 10;
				}
				if (pageNumber) {
					kfp.pageIndex = pageNumber;
				}
				else {
					kfp.pageIndex = paging.kalturaFilterPager.pageIndex;
				}
				
				var ce:ListItemsEvent = new ListItemsEvent(ListItemsEvent.LIST_USERS, model.usersFilter, kfp);
				ce.dispatch();
			}


			/**
			 * link to a .corp up-sell link
			 * */
			protected function getMoreUsers(event:MouseEvent):void {
				navigateToURL(new URLRequest(model.usersUpgradeLink),"_blank"); 
			}


			/**
			 * load users according to paging component value
			 * */
			protected function gotoPage():void {
				listUsers(paging.selectedPage);
			}
			
			
			/**
			 * get the edited user and update server
			 * */
			protected function saveUserData(e:Event):void {
				var ue:UserEvent;
				// need to know if new user or update user.
				if (model.drilldownMode == DrilldownMode.ADD) {
					// add user
					ue = new UserEvent(UserEvent.ADD_USER, _userDrilldown.user);
				}
				else if (model.drilldownMode == DrilldownMode.EDIT) {
					// edit user
					ue = new UserEvent(UserEvent.UPDATE_USER, _userDrilldown.user);
				}
				ue.dispatch();
			}
			
			
			/**
			 * hides and destroys the user drilldown window
			 * */
			protected function closeUserDrilldown(e:Event):void {
				if (_userDrilldown) {
					PopUpManager.removePopUp(_userDrilldown);
					_userDrilldown.removeEventListener(UserDrilldown.SAVE, saveUserData);
					_userDrilldown.removeEventListener(UserDrilldown.CLOSE, closeUserDrilldown);
					_userDrilldown.removeEventListener(UserDrilldown.ADD_ROLE, openAddRoleDialog);
					_userDrilldown = null;
					var de:DrilldownEvent = new DrilldownEvent(DrilldownEvent.USERS_SET_STATE, DrilldownMode.NONE);
					de.dispatch();
				}
			}
			
			
			/**
			 * popup the addrole window
			 * */
			protected function openAddRoleDialog(e:Event):void {
				_roleDrilldown = new RoleDrilldown();
				_roleDrilldown.init(new KalturaUserRole(), model.drilldownMode, rolesModel.partnerPermissionsUiconf, rolesModel.partnerPermissions);
				_roleDrilldown.addEventListener(RoleDrilldown.SAVE, saveRoleData);
				_roleDrilldown.addEventListener(RoleDrilldown.CLOSE, closeRoleDrilldown);
				PopUpManager.addPopUp(_roleDrilldown, this, true);
				PopUpManager.centerPopUp(_roleDrilldown);
			}
			
			
			/**
			 * hides and destroys the role drilldown window
			 * */
			protected function closeRoleDrilldown(e:Event):void {
				if (!_roleDrilldown)
					return;
				PopUpManager.removePopUp(_roleDrilldown);
				_roleDrilldown.removeEventListener(UserDrilldown.SAVE, saveRoleData);
				_roleDrilldown.removeEventListener(UserDrilldown.CLOSE, closeRoleDrilldown);
				_roleDrilldown = null;
			}
			
			
			/**
			 * get the new role data and update server
			 * */
			protected function saveRoleData(e:Event):void {
				var ue:RoleEvent = new RoleEvent(RoleEvent.ADD_ROLE, _roleDrilldown.role);
				ue.dispatch();
			}
			
			
			
			
			/**
			 * open drilldown window with data of currently selected user
			 * @return the newly created popup window
			 * */
			protected function drillToSelectedUser():UserDrilldown {
				var ud:UserDrilldown = new UserDrilldown();
				ud.init(rolesModel.roles, model.selectedUser, model.drilldownMode);
				ud.addEventListener(UserDrilldown.SAVE, saveUserData);
				ud.addEventListener(UserDrilldown.CLOSE, closeUserDrilldown);
				ud.addEventListener(UserDrilldown.ADD_ROLE, openAddRoleDialog);
				PopUpManager.addPopUp(ud, this, true);
				PopUpManager.centerPopUp(ud);
				return ud;
			}
			

			/**
			 * show the "add user" dialog
			 * */
			protected function openAddUserDialog(event:MouseEvent):void {
				// set the selected user to a new KalturaUser.
				var usr:KalturaUser = new KalturaUser();
				// kmc user definitions:
				usr.isAdmin = true;
				usr.loginEnabled = true;
				var ce:CairngormEvent = new UserEvent(UserEvent.SELECT_USER, usr);
				ce.dispatch();
				
				ce = new DrilldownEvent(DrilldownEvent.USERS_SET_STATE, DrilldownMode.ADD);
				ce.dispatch();
				
				// open drilldown for this user
				_userDrilldown = drillToSelectedUser();
			}


			/**
			 * show the "edit user" dialog
			 * */
			protected function openEditUserDialog(event:Event):void {
				var ce:DrilldownEvent = new DrilldownEvent(DrilldownEvent.USERS_SET_STATE, DrilldownMode.EDIT);
				ce.dispatch();
				_userDrilldown = drillToSelectedUser();
			}
			


			/**
			 * delete selected user, refresh quota data.
			 * */
			protected function deleteUser(event:Event):void {
				var ce:UserEvent = new UserEvent(UserEvent.DELETE_USER, model.selectedUser);
				ce.dispatch();
				var cg:CairngormEvent = new ListItemsEvent(ListItemsEvent.LIST_PARTNER_PERMISSIONS);
				cg.dispatch();
			}


			/**
			 * toggle between blocked / active stauses of the selected user 
			 * */
			protected function toggleUserStatus(event:Event):void {
				if (model.selectedUser.status != KalturaUserStatus.ACTIVE &&
					model.selectedUser.status != KalturaUserStatus.BLOCKED) {
					// this user is not active or blocked (probably deleted and shouldn't 
					// be in the table), so we can't toggle status
					dispatchEvent(new KmcErrorEvent(KmcErrorEvent.ERROR, ResourceManager.getInstance().getString('admin', 'cant_toggle')));
				}
				var ce:UserEvent = new UserEvent(UserEvent.TOGGLE_USER_STATUS, model.selectedUser);
				ce.dispatch();
			}


			/**
			 * select a user
			 * */
			protected function setSelectedUser(event:Event):void {
				var ce:UserEvent = new UserEvent(UserEvent.SELECT_USER, table.selectedUser);
				ce.dispatch();
			}
			
			protected function onHelp():void {
				dispatchEvent(new KmcHelpEvent(KmcHelpEvent.HELP, 'section43'));
			}
			
		]]>
	</mx:Script>
	<mx:HBox width="100%">
		<mx:Label text="{ResourceManager.getInstance().getString('admin', 'authorized_users')}" styleName="pageTitle"/>
		<mx:Spacer width="100%" />
		<mx:Button styleName="help" buttonMode="true" click="{onHelp()}"/>
	</mx:HBox>
	<mx:HBox styleName="usersTitleHbox">
		<mx:Label id="usersQuota" />
		<mx:LinkButton label="{ResourceManager.getInstance().getString('admin', 'upgrade')}" 
					   id="usersQuotaBtn" click="getMoreUsers(event)" styleName="LinkButton"/>
	</mx:HBox>

	<mx:HBox width="100%" >
		<mx:VBox id="tableContainer" width="100%" >
			<view:UsersTable id="table" width="100%" dataProvider="{model.users}" roles="{rolesModel.roles}" rowCount="10"
							 drillDown="openEditUserDialog(event)" deleteUser="deleteUser(event)" 
							 toggleBlock="toggleUserStatus(event)" selectUser="setSelectedUser(event)" />
			<controls:Paging id="paging" width="{table.width}" styleName="paging" nextPage="gotoPage()"
							 prvPage="gotoPage()" getPageNum="gotoPage()" rowsInPageChange="gotoPage()"
							 showRowsInPage="true" totalCount="{model.totalUsers}" />
		</mx:VBox>

		<mx:Spacer id="boxSpacer" width="5"/>

		<mx:VBox id="actionButtonsContainer" styleName="blueBox" height="100%" horizontalAlign="left">
			<mx:Button id="addBtn" label="{resourceManager.getString('admin','add_user')}" styleName="addPlst" width="100%"
					   height="30" buttonMode="true" click="openAddUserDialog(event)"/>
		</mx:VBox>
	</mx:HBox>
</mx:VBox>
