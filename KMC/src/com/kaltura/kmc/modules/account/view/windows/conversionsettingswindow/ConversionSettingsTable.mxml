<?xml version="1.0" encoding="utf-8"?>
<mx:DataGrid xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" rowHeight="30"
			 headerRelease="headerReleaseHandler(event)" selectable="false">
	<mx:Script>
		<![CDATA[
			import com.kaltura.kmc.modules.account.events.ConversionSettingsAccountEvent;
			import com.kaltura.kmc.modules.account.vo.ConversionProfileVO;
			import com.kaltura.kmc.modules.account.vo.FlavorVO;
			import com.kaltura.vo.KalturaThumbParams;

			import mx.collections.ArrayCollection;
			import mx.events.DataGridEvent;

			[Bindable]
			private var _allSelected:Boolean = false;
			
			/**
			 * @copy #cProfile
			 * */
			private var _cProfile:ConversionProfileVO;
			
			[Bindable]
			private var _thumbParamsData:ArrayCollection;

			/**
			 * @copy #dataEditable
			 * */
			private var _dataEditable:Boolean = true;


			public function get thumbParamsData():ArrayCollection {
				return _thumbParamsData;
			}


			public function set thumbParamsData(value:ArrayCollection):void {
				_thumbParamsData = value;
				validateProfileFlavors();
			}


			[Bindable]
			/**
			 * the conversion profile the table is currently showing
			 * */
			public function get cProfile():ConversionProfileVO {
				return _cProfile;
			}


			public function set cProfile(val:ConversionProfileVO):void {
				trace("setting _cProfile to", val.toString());
				_cProfile = val;
				validateProfileFlavors();
			}


			override public function set dataProvider(value:Object):void {
				// roles & permissions - set editable
				for each (var vo:FlavorVO in value) {
					vo.editable = _dataEditable;
				}
				super.dataProvider = value;
				validateProfileFlavors();
			}


			private function validateProfileFlavors():void {
				if (!_cProfile || !dataProvider || !thumbParamsData)
					return;

				var idsArr:Array = _cProfile.profile.flavorParamsIds.split(',');
				var currentFlavors:Array = new Array();
				var vo:FlavorVO;
				var isFlavor:Boolean;

				for each (var id:String in idsArr) {
					isFlavor = false;
					for each (vo in dataProvider) {
						if (vo.kFlavor.id == int(id)) {
							currentFlavors.push(id);
							isFlavor = true;
							break;
						}
					}
					if (!isFlavor) {
						for each (var thumbParam:KalturaThumbParams in thumbParamsData) {
							if (int(id) == thumbParam.id) {
								currentFlavors.push(id);
								break;
							}
						}
					}
				}

				var ids:String = currentFlavors.length == 0 ? null : currentFlavors.join(',');
				_cProfile.profile.flavorParamsIds = ids;
			}


			[Bindable]
			public function get allSelected():Boolean {
				return _allSelected;
			}


			public function set allSelected(allSelected:Boolean):void {
				_allSelected = allSelected;
			}


			private function headerReleaseHandler(event:DataGridEvent):void {
				event.preventDefault();
			}


			public function dispatchSelection(event:Event = null):void {
				var tsEvent:ConversionSettingsAccountEvent = new ConversionSettingsAccountEvent(ConversionSettingsAccountEvent.MARK_FLAVORS, !allSelected);
				tsEvent.dispatch();
			}


			public function isFlavorsSelected():Boolean {
				return !(_cProfile.profile.flavorParamsIds == null);
			}


			/**
			 * should data in the table be editable
			 * */
			public function get dataEditable():Boolean {
				return _dataEditable;
			}


			/**
			 * @private
			 */
			public function set dataEditable(value:Boolean):void {
				_dataEditable = value;
				for each (var vo:FlavorVO in dataProvider) {
					vo.editable = value;
				}
			}
		]]>
	</mx:Script>


	<mx:columns>
		<mx:DataGridColumn width="40"
						   itemRenderer="com.kaltura.kmc.modules.account.view.windows.conversionsettingswindow.renderers.FlavorSelectionRenderer"/>
		<mx:DataGridColumn headerText="Conversion Flavor" dataField="transcodingFlavor"
						   itemRenderer="com.kaltura.kmc.modules.account.view.windows.conversionsettingswindow.renderers.FlavorNameRenderer"/>
		<mx:DataGridColumn headerText="Description" dataField="description"
						   itemRenderer="com.kaltura.kmc.modules.account.view.windows.conversionsettingswindow.renderers.FlavorDescriptionRenderer"/>
		<mx:DataGridColumn headerText="Format" dataField="format"
						   itemRenderer="com.kaltura.kmc.modules.account.view.windows.conversionsettingswindow.renderers.FlavorFormatRenderer"/>
		<mx:DataGridColumn headerText="Codec" dataField="codec"
						   itemRenderer="com.kaltura.kmc.modules.account.view.windows.conversionsettingswindow.renderers.FlavorCodecRenderer"/>
		<mx:DataGridColumn headerText="Bitrate(kbps)" dataField="bitrate"
						   itemRenderer="com.kaltura.kmc.modules.account.view.windows.conversionsettingswindow.renderers.FlavorBitrateRenderer"/>
		<mx:DataGridColumn headerText="Dimensions" dataField="dimensions"
						   itemRenderer="com.kaltura.kmc.modules.account.view.windows.conversionsettingswindow.renderers.FlavorDimensionsRenderer"/>
		<!--<mx:DataGridColumn headerText="Transcoder" dataField="transcoder" itemRenderer="com.kaltura.kmc.modules.account.view.windows.conversionsettingswindow.renderers.FlavorTranscoderRenderer"/>-->
	</mx:columns>
</mx:DataGrid>
