<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" 
		 horizontalGap="0" horizontalAlign="center" verticalAlign="middle" horizontalScrollPolicy="off">
	<mx:LinkButton id="upBtn" click="moveItemUp(event)" styleName="moveUpButton" buttonMode="true"
				   toolTip="{resourceManager.getString('account','moveUp')}"/>
	<!--<mx:VRule height="20"/>-->
	<mx:LinkButton id="downBtn" click="moveItenDown(event)" styleName="moveDownButton" buttonMode="true"
				   toolTip="{resourceManager.getString('account','moveDown')}"/>
	<!--<mx:VRule height="20"/>-->
	<!--<  change to a different icon >-->
	<mx:LinkButton id="deleteBtn" click="deleteItem(event)" styleName="deleteButton" buttonMode="true"
				   toolTip="{resourceManager.getString('account','deleteField')}"/>
	
	<mx:Script>
		<![CDATA[
			import com.kaltura.kmc.modules.account.events.MetadataFieldEvent;
			import com.kaltura.vo.KMCMetadataProfileVO;
			import com.kaltura.vo.MetadataFieldVO;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			

			private var _metadataProfile:KMCMetadataProfileVO;
			private var _currentField:MetadataFieldVO
			
			override public function set data(value:Object):void {
				_currentField = value as MetadataFieldVO;
				_metadataProfile = parentDocument.metadataProfile;
			}
			
			private function moveItemUp(event:Event):void {
				var tempField:MetadataFieldVO;

				if (!_metadataProfile) {
					return;
				}
				
				var curIndex:int = _metadataProfile.metadataFieldVOArray.getItemIndex(_currentField);
				if (curIndex > 0) {
					tempField = _metadataProfile.metadataFieldVOArray.getItemAt(curIndex - 1).Clone();
					_metadataProfile.metadataFieldVOArray.removeItemAt(curIndex - 1);
					_metadataProfile.metadataFieldVOArray.addItemAt(tempField, curIndex);					
				}
				
				var reorderEvent:MetadataFieldEvent = new MetadataFieldEvent(MetadataFieldEvent.REORDER);
				reorderEvent.dispatch();
			}
			
			private function moveItenDown(event:Event):void {
				var tempField:MetadataFieldVO;
				
				if (!_metadataProfile) {
					return;
				}
				
				var curIndex:int = _metadataProfile.metadataFieldVOArray.getItemIndex(_currentField);
				if (curIndex < (_metadataProfile.metadataFieldVOArray.length-1)) {
					tempField = _metadataProfile.metadataFieldVOArray.getItemAt(curIndex + 1).Clone();
					_metadataProfile.metadataFieldVOArray.removeItemAt(curIndex + 1);
					_metadataProfile.metadataFieldVOArray.addItemAt(tempField, curIndex);					
				}
				
				var reorderEvent:MetadataFieldEvent = new MetadataFieldEvent(MetadataFieldEvent.REORDER);
				reorderEvent.dispatch();
			}
			
			/**
			 * On click on the "delete" field, the "are you sure" alert will pop up
			 * @param event
			 *
			 */
			private function deleteItem(event:Event):void {
				Alert.show(resourceManager.getString('account', 'metadataFieldDeleteAlert') + _currentField.name + '?', resourceManager.getString('account', 'metadataFieldDeleteTitle'), Alert.YES | Alert.NO, null, deleteResponseFunc);
			}
			
			/**
			 * This function handles the case of "yes" selection and deletes the selected fields
			 * */
			private function deleteResponseFunc(evt:CloseEvent):void {
				if (evt.detail == Alert.YES) {
					_metadataProfile.metadataFieldVOArray.removeItemAt(_metadataProfile.metadataFieldVOArray.getItemIndex(_currentField));
					var deleteMetadataField:MetadataFieldEvent = new MetadataFieldEvent(MetadataFieldEvent.DELETE, _currentField);
					deleteMetadataField.dispatch();
				}
			}

		]]>
	</mx:Script>
</mx:HBox>

