<?xml version="1.0" encoding="utf-8"?>
<HelpTitleWindow xmlns="com.kaltura.containers.*" xmlns:mx="http://www.adobe.com/2006/mxml" width="420" 
				 styleName="TitleWindowType2" creationComplete="creationCompleteHandler(event)"
				 title="{resourceManager.getString('account','conversionFlavorSettingsTitle')}" 
				 showCloseButton="true" close="{onClose()}">
	<mx:Script>
		<![CDATA[
			import com.kaltura.kmc.business.PermissionManager;
			import com.kaltura.kmc.modules.account.vo.FlavorVO;
			import com.kaltura.types.KalturaAssetParamsOrigin;
			import com.kaltura.types.KalturaFlavorReadyBehaviorType;
			import com.kaltura.types.KalturaNullableBoolean;
			import com.kaltura.vo.KalturaConversionProfileAssetParams;
			
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			import mx.resources.ResourceManager;


			private const LABEL_WIDTH:int = 200;
			private const ELEMENT_WIDTH:int = 180;
			
			
			/**
			 * data provider for the asset's ready behaviour
			 * */
			private const _availabilityOptions:Array = [
				{value:KalturaFlavorReadyBehaviorType.INHERIT_FLAVOR_PARAMS, name:ResourceManager.getInstance().getString('account', 'flavor_inherit')}, 
				{value:KalturaFlavorReadyBehaviorType.REQUIRED, name:ResourceManager.getInstance().getString('account', 'flavor_required')}, 
				{value:KalturaFlavorReadyBehaviorType.OPTIONAL, name:ResourceManager.getInstance().getString('account', 'flavor_optional')}
			];
			
			/**
			 * data provider for the asset's origin
			 * */
			private const _originOptions:Array = [
				{value:KalturaAssetParamsOrigin.CONVERT, name:ResourceManager.getInstance().getString('account', 'flavor_convert')},
				{value:KalturaAssetParamsOrigin.CONVERT_WHEN_MISSING, name:ResourceManager.getInstance().getString('account', 'flavor_cwm')},
				{value:KalturaAssetParamsOrigin.INGEST, name:ResourceManager.getInstance().getString('account', 'flavor_ingest')}
			];
			
			/**
			 * data provider for the asset's generation policy
			 * */
			private const _genOptions:Array = [
				{value:KalturaNullableBoolean.FALSE_VALUE, name:ResourceManager.getInstance().getString('account', 'gen_no_force')},
				{value:KalturaNullableBoolean.TRUE_VALUE, name:ResourceManager.getInstance().getString('account', 'gen_force')},
			];
			
			
			
			
			private var _cpap:KalturaConversionProfileAssetParams;
			

			[Bindable]
			public var profileName:String;

			/**
			 * flavor data
			 * */
			private var _flavorVO:FlavorVO;


			/**
			 * remove the popup
			 * */
			private function onClose():void {
				PopUpManager.removePopUp(this);
			}

			/**
			 * edit cpap and mark it as dirty
			 * */
			private function save():void {
				_cpap.forceNoneComplied = cbGenPolicy.selectedItem.value;
				_cpap.readyBehavior = cbAvailability.selectedItem.value;
				_cpap.origin = cbConvertPolicy.selectedItem.value;
				_cpap.systemName = tiSystemName.text;
				// when saving the profile, we look for the "dirty" attribute on the cpaps
				_cpap.dirty = true;
				onClose();
			}

			
			/**
			 * show the values from the cpap in the window
			 * */
			protected function setValues():void {
				if (cbAvailability && cbConvertPolicy && tiSystemName && cbGenPolicy) {
					// source flavor - always ingest, inherit
					if (_flavorVO.kFlavor.tags.indexOf("source") > -1) {
						_cpap.origin = KalturaAssetParamsOrigin.INGEST;
						_cpap.readyBehavior = KalturaFlavorReadyBehaviorType.INHERIT_FLAVOR_PARAMS;
						cbConvertPolicy.enabled = false;
					}
					if (_flavorVO.kFlavor.id == 0) {
						cbGenPolicy.enabled = false;
						_cpap.forceNoneComplied = KalturaNullableBoolean.FALSE_VALUE;
					}
					// default values:
					if (_cpap.readyBehavior == int.MIN_VALUE) {
						_cpap.readyBehavior = KalturaFlavorReadyBehaviorType.OPTIONAL/* _flavorVO.kFlavor.?? */;
						//TODO in server there's a readyBehavior value on the KalturaFlavorParams, if they expose it we should use it (Atar).
					}
					if (_cpap.origin == int.MIN_VALUE) {
						_cpap.origin = KalturaAssetParamsOrigin.CONVERT;
					}
					if (!_cpap.systemName) {
						_cpap.systemName = _flavorVO.kFlavor.systemName;
					}
					if (_cpap.forceNoneComplied == int.MIN_VALUE) {	// KalturaNullableBoolean is generated in the client as int
						_cpap.forceNoneComplied = KalturaNullableBoolean.FALSE_VALUE;
					}
					cbAvailability.selectedIndex = getItemIndex(_availabilityOptions, _cpap.readyBehavior);
					cbConvertPolicy.selectedIndex = getItemIndex(_originOptions, _cpap.origin);
					cbGenPolicy.selectedIndex = getItemIndex(_genOptions, _cpap.forceNoneComplied);
					tiSystemName.text = _cpap.systemName;
				}
			}
			
			
			/**
			 * @param ar	list of stuff
			 * @param value	the value we are looking for
			 * @return the index of the item whose "value" property equals <code>value</code>
			 * */
			protected function getItemIndex(ar:Array, value:*):int {
				for (var i:int = 0; i<ar.length; i++) {
					if (ar[i].value == value) {
						return i;
					}
				}
				return -1;
			}
			
			
			
			protected function creationCompleteHandler(event:FlexEvent):void {
				if (_cpap) {
					setValues();
				}
				PermissionManager.getInstance().applyAllAttributes(this, "conversionFlavorDrilldown");
			}

			/**
			 * flavor data
			 * */
			public function get flavorVO():FlavorVO {
				return _flavorVO;
			}


			[Bindable]
			/**
			 * @private
			 * */
			public function set flavorVO(value:FlavorVO):void {
				_flavorVO = value;
			}

			/**
			 * the KalturaConversionProfileAssetParams being edited
			 */
			public function get cpap():KalturaConversionProfileAssetParams {
				return _cpap;
			}

			/**
			 * @private
			 */
			public function set cpap(value:KalturaConversionProfileAssetParams):void {
				_cpap = value;
				setValues();
			}



		]]>
	</mx:Script>
	<mx:HBox>
		<mx:Label text="{resourceManager.getString('account','convProfileName')}" 
				  styleName="boldLabel" width="{LABEL_WIDTH}"/>
		<mx:Label text="{profileName}"  />
	</mx:HBox>
	<mx:HBox>
		<mx:Label text="{resourceManager.getString('account','convFlavorName')}" 
				  styleName="boldLabel" width="{LABEL_WIDTH}"/>
		<mx:Label text="{flavorVO.kFlavor.name}"  />
	</mx:HBox>
	<mx:HBox>
		<mx:Label text="{resourceManager.getString('account','assetAvialability')}" 
				  styleName="boldLabel" width="{LABEL_WIDTH}"/>
		<mx:ComboBox id="cbAvailability" dataProvider="{_availabilityOptions}" 
					 labelField="name" width="{ELEMENT_WIDTH}" />
	</mx:HBox>
	<mx:HBox>
		<mx:Label text="{resourceManager.getString('account','convertToFlavor')}" 
				  styleName="boldLabel" width="{LABEL_WIDTH}"/>
		<mx:ComboBox id="cbConvertPolicy" dataProvider="{_originOptions}" 
					 labelField="name" width="{ELEMENT_WIDTH}" />
	</mx:HBox>
	<mx:HBox>
		<mx:Text htmlText="{resourceManager.getString('account','systemName')}" width="{LABEL_WIDTH}"/>
		<mx:TextInput id="tiSystemName" width="{ELEMENT_WIDTH}" />
	</mx:HBox>
	<mx:HBox>
		<mx:Label text="{resourceManager.getString('account','generationPolicy')}" 
				  styleName="boldLabel" width="{LABEL_WIDTH}"/>
		<mx:ComboBox id="cbGenPolicy" dataProvider="{_genOptions}" 
					 labelField="name" width="{ELEMENT_WIDTH}" />
	</mx:HBox>
	<mx:ControlBar width="100%" horizontalAlign="center">
		<mx:Button id="submitBtn" label="{resourceManager.getString('account','saveFlavor')}" useHandCursor="true"
				   buttonMode="true" click="save()"/>
	</mx:ControlBar>
</HelpTitleWindow>
