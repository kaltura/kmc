<?xml version="1.0" encoding="utf-8"?>
<renderers:ConversionProfileRendererBase  
	xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" verticalAlign="middle" 
	verticalScrollPolicy="off" horizontalScrollPolicy="off"  paddingLeft="4" 
	xmlns:renderers="com.kaltura.kmc.modules.account.view.windows.conversion.renderers.*">
	
	<mx:Script>
		<![CDATA[
			import com.kaltura.kmc.modules.account.model.AccountModelLocator;
			import com.kaltura.kmc.modules.account.view.windows.conversion.ConversionProfilesTable;
			import com.kaltura.kmc.modules.account.vo.ConversionProfileVO;
			import com.kaltura.vo.FlavorVO;
			
			import mx.collections.ArrayCollection;
			
			private var _model:AccountModelLocator = AccountModelLocator.getInstance();
			
			[Bindable]
			private var _text:String = '';
			
			[Bindable]
			private var _tooltip:String = '';
			
			
			override public function set data(value:Object):void {
				super.data = value; // value is ConversionProfileVO
				setFlavorsName();
			}
			
			
			private function setFlavorsName():void
			{
				var profile:ConversionProfileVO = data as ConversionProfileVO;
				
				if(profile.profile.flavorParamsIds == null) {
					_text = '';
					return; // in case there are no flavors for the current profile
				}
				var flavors:ArrayCollection = (_listData.owner as ConversionProfilesTable).flavorsSet;
				var idsArr:Array = profile.profile.flavorParamsIds.split(',');
				var flavorNames:Array = new Array();
				for each(var id:String in idsArr) { // for each of the profile's flavor ids
					for each(var flavor:FlavorVO in flavors) { // for each of the flavors in the system
						if(int(id) == flavor.kFlavor.id) {
							flavorNames.push(flavor.kFlavor.name);
							break;
						}
					}
				}
				
				_text = flavorNames.join(", ");
				_tooltip = flavorNames.join("\n");
			}
			
		]]>
	</mx:Script>


	<mx:Label id='flavorsLabel' text="{_text}" toolTip="{_tooltip}"/>
</renderers:ConversionProfileRendererBase>
