<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" verticalAlign="middle" horizontalScrollPolicy="off">
	<mx:Script>
		<![CDATA[
			import com.kaltura.kmc.modules.content.view.window.flavors.FilesTable;
			import com.kaltura.kmc.modules.content.vo.UploadFileVo;
			import com.kaltura.vo.KalturaFlavorParams;
			
			import mx.events.ListEvent;


			/**
			 * Object of type <code>UploadFileVo</code>
			 * */
			override public function set data(value:Object):void {
				super.data = value;
				if (value) {
					cb.dataProvider = value.flavors; // KalturaFlavorParams objects
					selectFlavor(value as UploadFileVo);
				}
			}


			/**
			 * for when changing only a single flavor (?)
			 * show the correct flavorParamId in the CB.
			 * otherwise use the file name pattern to "guess" flavor.
			 * */
			protected function selectFlavor(ufv:UploadFileVo):void {
				var found:Boolean;
				var o:KalturaFlavorParams;
				
				// if a flavor was defined for this file (vo)
				for each(o in ufv.flavors) {
					if (o.id.toString() == ufv.flavorParamId) {
						cb.selectedItem = o;
						found = true;
						break;
					}
				}
				// if no flavor was defined, try matching by flavor system name
				if (!found) {
					// use the flavor system name and file name to "guess" flavor.
					for each(o in ufv.flavors) {
						if (ufv.fileName.indexOf(o.systemName) != -1) {
							cb.selectedItem = o;
							ufv.flavorParamId = o.id.toString();
							found = true;
							break;
						}
					}
				}
				// if that failed too, select the first flavor in the list.
				if (!found) {
					cb.selectedIndex = 0;
					if (ufv.flavors) {
						ufv.flavorParamId = ufv.flavors.getItemAt(0).id.toString();
					}
					else {
						ufv.flavorParamId = '';
					}
				}
				
			}


			protected function combobox1_changeHandler(event:ListEvent):void {
				dispatchEvent(new Event(FilesTable.FLAVOR_SELECTED, true));
			}


			public function get selectedFlavor():KalturaFlavorParams {
				return cb.selectedItem as KalturaFlavorParams;
			}


//			public function set selectedFlavor(value:KalturaFlavorParams):void {
//				cb.selectedItem = value;
//			}
		]]>
	</mx:Script>
	<mx:ComboBox id="cb" change="combobox1_changeHandler(event)" labelField="name" width="100%"/>
</mx:HBox>
