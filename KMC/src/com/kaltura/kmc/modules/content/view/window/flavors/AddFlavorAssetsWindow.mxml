<?xml version="1.0" encoding="utf-8"?>
<!---
	A popup window that allows matching files with the flavor they represent. 
	The same window with different configurations will be used for upload, link and import.
-->
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" 
		xmlns:containers="com.kaltura.containers.*" 
		xmlns:flavors="com.kaltura.kmc.modules.content.view.window.flavors.*"
		width="550" height="400" styleName="TitleWindowType2" 
		showCloseButton="true" close="closeHandler(event)"
		creationComplete="titlewindow1_creationCompleteHandler(event)" >

	<mx:Script>
		<![CDATA[
			import com.kaltura.kmc.modules.content.events.MediaEvent;
			import com.kaltura.kmc.modules.content.model.CmsModelLocator;
			import com.kaltura.kmc.modules.content.view.window.entrydetails.EntryFlavors;
			import com.kaltura.kmc.modules.content.vo.ConversionProfileWithFlavorParamsVo;
			import com.kaltura.kmc.modules.content.vo.UploadFileVo;
			import com.kaltura.managers.FileUploadManager;
			import com.kaltura.types.KalturaAssetParamsOrigin;
			import com.kaltura.types.KalturaEntryStatus;
			import com.kaltura.types.KalturaFlavorReadyBehaviorType;
			import com.kaltura.types.KalturaMediaType;
			import com.kaltura.vo.KalturaAssetParamsResourceContainer;
			import com.kaltura.vo.KalturaAssetsParamsResourceContainers;
			import com.kaltura.vo.KalturaBaseEntry;
			import com.kaltura.vo.KalturaConversionProfileAssetParams;
			import com.kaltura.vo.KalturaMediaEntry;
			import com.kaltura.vo.KalturaUrlResource;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.resources.ResourceManager;
			
			/*
				Events
			*/
			public static const CLOSE_WINDOW:String = "closeWindow";
			
			
			/*
				window states
			*/
			public static const SINGLE_FILE_STATE:String = "single_file_state"; 
			public static const MULTI_FILES_STATE:String = "multi_files_state";
			
			/*
				actions
			*/
			public static const ACTION_UPLOAD:String = "action_upload";
			public static const ACTION_IMPORT:String = "action_import";
			public static const ACTION_LINK:String = "action_link";
			
			[Bindable]
			/**
			 * windowState is the state the window is currently functioning at.
			 * MULTI_FILES_STATE means this is replacement of all flavours of the entry,
			 * while SINGLE_FILE_STATE means we only replace a single flavour.
			 * note that this is not affected by the partner multiple files 
			 * ingestion permission, and shouldn't be.
			 * */
			public var windowState:String;
			
			[Bindable]
			/**
			 * @copy #windowAction
			 * */
			private var _windowAction:String;
			
			[Bindable]
			/**
			 * hide the "add more files" options,
			 * */
			public var allowAddFiles:Boolean = true;
			
			
			/**
			 * @copy #selectedCPAssetParams
			 * */
			protected var _selectedCPAssetParams:ArrayCollection;
			
			private var _fileReferenceList:FileReferenceList;
			
			/**
			 * @copy #entry
			 * */
			private var _entry:KalturaBaseEntry;
			
			[Bindable]
			/**
			 * list of available conversion profiles and their
			 * respective flavorparam ids
			 * <code>ConversionProfileWithFlavorParamsVo</code> objects
			 * */
			public var conversionProfiles:ArrayCollection;
			
			[Bindable]
			/**
			 * <code>UploadFileVo</code> objects. <br>
			 * when passed from outside the objects only have fileData and fileName attributes popuplated. <br>
			 * flavors attribute is set in FilesTable.(set)flavors.<br>
			 * flavorParamId attribute is initialized in FlavorRenderer.selectFlavor and set 
			 * 		in FilesTable.flavorSelected
			 * */
			public var filesList:ArrayCollection;


			/**
			 * make sure the same flavour is not selected for more than one file
			 * ---------------------------------- 
			 * • more than one file is associated to the same conversion flavor settings.
			 * • Some of the files are exceeding system upload file size limitation 
			 * 	(currently set to X GB). please import these files to the system by 
			 * 	utilizing other ingestion options, applicable in your account (e.g. 
			 * 	import, drop folder etc)" 
			 * • Some files required for entry’s readiness for publishing are missing.
			 * */
			protected function validate():Boolean {
				var result:Boolean = true;
				if (!validateMultipleFilesSingleFlavour()) {
					Alert.show(ResourceManager.getInstance().getString('cms', 'multipleFilesFlavor'), 
						ResourceManager.getInstance().getString('cms', 'error'));
					result = false;
				}
				// only test file size when in upload mode
				else if (_windowAction == AddFlavorAssetsWindow.ACTION_UPLOAD && !validateFileSize()) {
					Alert.show(ResourceManager.getInstance().getString('cms', 'filesTooBig', [getPartnerUploadLimit()]), 
						ResourceManager.getInstance().getString('cms', 'error'));
					result = false;
				}
				return result;
			}
			
			
			protected function getPartnerUploadLimit():Number {
				return CmsModelLocator.getInstance().extSynModel.partnerData.maxUploadSize;
			}
			
			/**
			 * if file size of any file exceeds partner limit, return false
			 * */
			protected function validateFileSize():Boolean {
				var result:Boolean = true;
				var bytesInMega:int = 1024*1024;
				var maxSize:Number = getPartnerUploadLimit();
				for each (var file:UploadFileVo in filesList) {
					if ((file.fileData.size/bytesInMega) > maxSize) {
						result = false;
						break;
					}
				}
				return result;
			}
			
			
			/**
			 * if the same flavour is selected for more than one file, return false.
			 * */
			protected function validateMultipleFilesSingleFlavour():Boolean {
				// when replaceing a single flavour, this is not a problem.
				if (!allowAddFiles) return true;
				// scan files list and put each flavor id in an array.
				// if the same flavor appears twice - fail, else return true.
				var ar:Vector.<String> = new Vector.<String>();
				for each (var file:UploadFileVo in filesList) {
					for (var i:int = 0; i<ar.length; i++) {
						if (file.flavorParamId == ar[i]) {
							return false;
						}
					}
					ar.push(file.flavorParamId);
				}
				return true;
			}
			
			
			/**
			 * run validations sequence
			 * */
			protected function initSubmitSequence(event:MouseEvent):void {
				if (validate()) {
					validateReadyCriteria();
				}
			}
			
			
			/**
			 * test entry readiness criteria, approve / cancel, continue accordingly.
			 * */
			protected function validateReadyCriteria():void {
				var isOk:Boolean = true;
				var cpaps:Array = (cnvrtProfCb.selectedItem as ConversionProfileWithFlavorParamsVo).flavors.source;
				
				// scan the flavors list and see if any required flavors are missing
				for each(var cpap:KalturaConversionProfileAssetParams in cpaps) {
					var bFound:Boolean = false;
					if (cpap.readyBehavior == KalturaFlavorReadyBehaviorType.REQUIRED && cpap.origin == KalturaAssetParamsOrigin.INGEST) {
						for each (var fuv:UploadFileVo in filesList) {
							if (fuv.flavorParamId == cpap.assetParamsId.toString()) {
								bFound = true;
								break;
							}
						}
						if (!bFound) {
							isOk = false;
							break;
						}
					}
				}
				if (isOk) {
					// save
					submit();
				}
				else {
					// alert user
					Alert.show(ResourceManager.getInstance().getString('cms', 'missingRequired'),
						ResourceManager.getInstance().getString('cms', 'warning'), Alert.OK|Alert.CANCEL, null, handleWarning);
				}
				
			}
			
			
			/**
			 * if the user wishes to upload partial files list, do so.
			 * */
			protected function handleWarning(e:CloseEvent):void {
				if (e.detail == Alert.OK) {
					submit();
				}
			}
			
				
			
			/**
			 * Submit handler:
			 * 	•	Upload – 
			 * 	•	Import – trigger media.add with matching KalturaURLResource objects.
			 * 	•	Link – trigger media.add with matching KalturaNetStorageResource objects.
			 * */
			protected function submit():void {
				switch (_windowAction){
					case AddFlavorAssetsWindow.ACTION_UPLOAD:
						submitUpload();
						break;
					case AddFlavorAssetsWindow.ACTION_IMPORT:
						submitImport();
						break;
					case AddFlavorAssetsWindow.ACTION_UPLOAD:
						submitUpload();
						break;
					
				}
				// close the popup
				closeHandler();
			}
			
			
			protected function submitImport():void {
				var n:int = filesList.length;
				var ufv:UploadFileVo;
				// conversion profile id
				var cnvrtProfId:String = (cnvrtProfCb.selectedItem as ConversionProfileWithFlavorParamsVo).profile.id.toString();
				
				// add uploads to the FileUploadManager
				if (windowState == AddFlavorAssetsWindow.MULTI_FILES_STATE) {
					// the actual resource we send is a list of the containers for the resources we want to replace.                
					var mediaResource:KalturaAssetsParamsResourceContainers = new KalturaAssetsParamsResourceContainers();
					mediaResource.resources = new Array();
					
					for each (ufv in filesList) {
						// the first resource of the flavor we want to replace
						var subSubResource:KalturaUrlResource = new KalturaUrlResource();
						subSubResource.url = ufv.fileName;
						
						// container for the resource we want to replace
						var subResource:KalturaAssetParamsResourceContainer = new KalturaAssetParamsResourceContainer();
						subResource.resource = subSubResource;
						subResource.assetParamsId = parseInt(ufv.flavorParamId);
						
						// add to list
						mediaResource.resources.push(subResource);
						
					}
					// dispatch the event with required data
					var ce:MediaEvent = new MediaEvent(MediaEvent.UPDATE_MEDIA, _entry as KalturaMediaEntry);
					ce.data = {conversionProfileId:cnvrtProfId, resource:mediaResource};
					ce.dispatch();
				}
				else {
					// only one file
					ufv = filesList[0] as UploadFileVo;
					var resource:KalturaUrlResource;
					var dffe:MediaEvent;
					if (ufv.flavorAssetId != null) {
						// updating a single flavor
						resource = new KalturaUrlResource();
						resource.url = ufv.fileName;
						dffe = new MediaEvent(MediaEvent.UPDATE_SINGLE_FLAVOR, _entry as KalturaMediaEntry);
						dffe.data = {flavorAssetId: ufv.flavorAssetId, resource:resource};
						dffe.dispatch();
					}
					else {
						// adding a single flavor
						resource = new KalturaUrlResource();
						resource.url = ufv.fileName;
						dffe = new MediaEvent(MediaEvent.ADD_SINGLE_FLAVOR, _entry as KalturaMediaEntry);
						dffe.data = {flavorParamsId: ufv.flavorParamId, resource:resource};
						dffe.dispatch();
					}
				}
			}
			
			
			
			/**
			 * add the relevant files to FileUploadManager.
			 * */
			protected function submitUpload():void {
				var n:int = filesList.length;
				var uploader:FileUploadManager = FileUploadManager.getInstance();
				var ufv:UploadFileVo;
				// conversion profile id
				var cnvrtProfId:String = (cnvrtProfCb.selectedItem as ConversionProfileWithFlavorParamsVo).profile.id.toString();
				
				// add uploads to the FileUploadManager
				if (windowState == AddFlavorAssetsWindow.MULTI_FILES_STATE) {
					var files:Array = new Array();
					for (var i:int = 0; i<n; i++) {
						ufv = filesList[i] as UploadFileVo;
						files.push(uploader.createFuv(_entry.id, ufv.fileData, ufv.flavorParamId, ufv.flavorAssetId, cnvrtProfId));
					}
					// decide the correct action (add / update)
					uploader.updateEntryContent(_entry.id, files);
				}
				else {
					// only one file
					ufv = filesList[0] as UploadFileVo;
					uploader.updateEntryFlavor(_entry.id, uploader.createFuv(_entry.id, ufv.fileData, ufv.flavorParamId, ufv.flavorAssetId, cnvrtProfId), ufv.flavorAssetId == null);
				}
			}
			
			
			
			protected function cnvrtPrfLbl(itemObj:ConversionProfileWithFlavorParamsVo):String{
				return itemObj.profile.name;
			}

			
			/**
			 * remove the popup
			 * */
			protected function closeHandler(event:CloseEvent = null):void {
				dispatchEvent(new Event(CLOSE_WINDOW));
			}
			
			/**
			 * open the file browser 
			 * */
			protected function addMoreFiles():void {
				_fileReferenceList = new FileReferenceList();
				_fileReferenceList.addEventListener(Event.SELECT, onFlavorsSelected);
				var mediaType:int = parseInt(_entry.type);
				if (mediaType == KalturaMediaType.VIDEO) 
					_fileReferenceList.browse(new Array(getTypeFilter('video',EntryFlavors.VIDEO_TYPES)));
				else if (mediaType == KalturaMediaType.AUDIO)
					_fileReferenceList.browse(new Array(getTypeFilter('audio',EntryFlavors.AUDIO_TYPES)));
			}
			
			/**
			 * add the selected files to the files list
			 * */
			private function onFlavorsSelected(event:Event):void {
				_fileReferenceList.removeEventListener(Event.SELECT, onFlavorsSelected);
				var files:Array = (event.target as FileReferenceList).fileList;
				for each (var fileReference:FileReference in files) {
					var uploadFile:UploadFileVo = new UploadFileVo();
					uploadFile.fileName = fileReference.name;
					uploadFile.fileData = fileReference;
					filesList.addItem(uploadFile);
				}
				// trigger the binding to get the rest of the fileVo attributes set
				filesTable.dataProvider = filesList;
			}
			
			/**
			 * will return fileFilter according to given name and file types
			 * */
			private function getTypeFilter(localeKey:String, types:String):FileFilter {
				return new FileFilter(resourceManager.getString('cms',localeKey) +  "(" + types+ ")" , types );
			}


			protected function titlewindow1_creationCompleteHandler(event:FlexEvent):void {
				this.mx_internal::closeButton.buttonMode = true;
				var bFound:Boolean;		// found entry ingestion profile?
				var defInd:int = -1;	// index of partner default ingestion profile
				var vo:ConversionProfileWithFlavorParamsVo;
				if (_entry && conversionProfiles) {
					// select the conversion profile that matches the entry's default
					for (var i:int = 0; i<conversionProfiles.length; i++) {
						vo = conversionProfiles[i] as ConversionProfileWithFlavorParamsVo; 
						if (vo.profile.id.toString() == _entry.ingestionProfileId) {
							cnvrtProfCb.selectedIndex = i;
							bFound = true;
							break;
						} 
						if (vo.profile.isDefault) {
							defInd = i;
						}
					}
					// if not found, use partner default.
					if (!bFound) {
						cnvrtProfCb.selectedIndex = defInd;
					}
				}
				// set appearance according to action
				setWindowAppearance(_windowAction);
			}
			
			
			/**
			 * set texts according to selected action
			 * @param action	one of the AddFlavorAssetsWindow.ACTION_XX consts
			 * */
			private function setWindowAppearance(action:String) :void {
				if (!tableHeader) {
					// not creationComplete yet
					return;
				} 
				switch (action) {
					case AddFlavorAssetsWindow.ACTION_IMPORT:
						tableHeader.text = ResourceManager.getInstance().getString('cms', 'fileUrls')
						submitBtn.label = ResourceManager.getInstance().getString('cms', 'submitImportLabel')
						break;
					case AddFlavorAssetsWindow.ACTION_LINK:
						tableHeader.text = ResourceManager.getInstance().getString('cms', 'fileUrls')
						submitBtn.label = ResourceManager.getInstance().getString('cms', 'submitLinkLabel')
						break;
					case AddFlavorAssetsWindow.ACTION_UPLOAD:
						tableHeader.text = ResourceManager.getInstance().getString('cms', 'uploadedFiles')
						submitBtn.label = ResourceManager.getInstance().getString('cms', 'submitUploadLabel')
						break;
				}
			}
			
			
			/**
			 * windowAction represents the action that will be taken upon 
			 * "submit" click. applicable values are either ACTION_UPLOAD, ACTION_IMPORT
			 * or ACTION_LINK.
			 * */
			public function get windowAction():String {
				return _windowAction;
			}
			
			/**
			 * @private
			 */
			public function set windowAction(value:String):void {
				_windowAction = value;
				setWindowAppearance(value);
			}

			/**
			 * the entry being edited
			 * */
			public function get entry():KalturaBaseEntry {
				return _entry;
			}

			/**
			 * @private
			 */
			public function set entry(value:KalturaBaseEntry):void {
				_entry = value;
				if (value.status == KalturaEntryStatus.NO_CONTENT) {
					title = ResourceManager.getInstance().getString('cms', 'windowTitleAdd');
				}
				else {
					title = ResourceManager.getInstance().getString('cms', 'windowTitleUpdate');
				}
			}
			

		]]>
	</mx:Script>
	
	<mx:HBox verticalAlign="middle" visible="{windowState == AddFlavorAssetsWindow.MULTI_FILES_STATE}" 
			 includeInLayout="{windowState == AddFlavorAssetsWindow.MULTI_FILES_STATE}">
		<mx:Label text="{ResourceManager.getInstance().getString('cms', 'convProfLbl')}:" 
				  styleName="simpleBoldText"/>
		<mx:ComboBox id="cnvrtProfCb" labelFunction="cnvrtPrfLbl" 
					 selectedIndex="0" dataProvider="{conversionProfiles}" />
	</mx:HBox>
	<mx:Label id="tableHeader" text="{ResourceManager.getInstance().getString('cms', 'uploadedFiles')}" 
			  styleName="tableName"/>
	<flavors:FilesTable id="filesTable" width="100%" flavors="{cnvrtProfCb.selectedItem.flavors}"
						dataProvider="{filesList}" allowAddFiles="{allowAddFiles}"
						addFiles="addMoreFiles()" showSize="{_windowAction == AddFlavorAssetsWindow.ACTION_UPLOAD}" 
						nameEditable="{_windowAction != AddFlavorAssetsWindow.ACTION_UPLOAD}"/>
	<mx:ControlBar>
		<mx:Button id="submitBtn" click="initSubmitSequence(event)" buttonMode="true"
				   label="{ResourceManager.getInstance().getString('cms', 'submitUploadLabel')}"/>
	</mx:ControlBar>
</mx:TitleWindow>
