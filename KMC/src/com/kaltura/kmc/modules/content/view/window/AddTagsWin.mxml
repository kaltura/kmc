<?xml version="1.0" encoding="utf-8"?>
<!---
	window used to add and modify entry/category tags
-->
<HelpTitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" xmlns="com.kaltura.containers.*"
				 layout="vertical"  help="{onHelp()}" showCloseButton="true"
				 title="{resourceManager.getString('cms', 'addTags')}">

	<mx:Script>
		<![CDATA[
			import com.adobe.cairngorm.control.CairngormEvent;
			import com.kaltura.analytics.GoogleAnalyticsConsts;
			import com.kaltura.analytics.GoogleAnalyticsTracker;
			import com.kaltura.analytics.KAnalyticsTracker;
			import com.kaltura.analytics.KAnalyticsTrackerConsts;
			import com.kaltura.kmc.events.KmcHelpEvent;
			import com.kaltura.kmc.modules.content.events.CategoryEvent;
			import com.kaltura.kmc.modules.content.events.EntriesEvent;
			import com.kaltura.types.KalturaStatsKmcEventType;
			
			import mx.collections.ArrayCollection;
			import mx.core.mx_internal;

			
			public static const OBJECT_TYPE_ENTRY:String = "entry";
			public static const OBJECT_TYPE_CATEGORY:String = "category";
			
			[Bindable]
			/**
			 * optional values: "entry" / "category"
			 * */
			public var objectType:String; 
			
			
			private var _objects:Array = new Array();
			
			
			private function onHelp():void {
				if (objectType == OBJECT_TYPE_ENTRY) {
					dispatchEvent(new KmcHelpEvent(KmcHelpEvent.HELP, 'section_add_entry_tags'));
				}
				else if (objectType == OBJECT_TYPE_CATEGORY) {
					dispatchEvent(new KmcHelpEvent(KmcHelpEvent.HELP, 'section_add_cat_tags'));
				}
			}



			/**
			 * save modified entries
			 * */
			private function save():void {
				if (_objects) {
					for (var i:int = 0; i < _objects.length; i++) {
						if (_objects[i].tags) {
							_objects[i].tags = _objects[i].tags + "," + tags.text;
						}
						else {
							_objects[i].tags = tags.text;
						}
					}
				}
				
				var cgEvent:CairngormEvent;
				if (objectType == OBJECT_TYPE_ENTRY) {
					cgEvent = new EntriesEvent(EntriesEvent.UPDATE_ENTRIES, new ArrayCollection(_objects));
					cgEvent.dispatch();
	
					for each (var ent:Object in _objects) {
						KAnalyticsTracker.getInstance().sendEvent(KAnalyticsTrackerConsts.CONTENT,KalturaStatsKmcEventType.CONTENT_ADD_TAGS,
																  "Entry>AddTagsWindow", ent.id);
					}
					GoogleAnalyticsTracker.getInstance().sendToGA(GoogleAnalyticsConsts.CONTENT_ADD_TAGS,GoogleAnalyticsConsts.CONTENT);
				}
				else {
					cgEvent = new CategoryEvent(CategoryEvent.UPDATE_CATEGORIES);
					cgEvent.data = _objects;
					cgEvent.dispatch();
				}
				// close window
				mx_internal::closeButton.dispatchEvent(new MouseEvent(MouseEvent.CLICK));
			}



			
			public function setObjects(arr:Array):void {
				_objects = arr;
			}

			private function getDescription(objectType:String):String {
				if (objectType == OBJECT_TYPE_ENTRY) {
				 	return resourceManager.getString('cms', 'howToEnterTags')
				}
				else {
					return resourceManager.getString('cms', 'howToEnterTagsCats')
				}
			}
			
		]]>
	</mx:Script>
	<mx:Text id="addTagsText" text="{getDescription(objectType)}"/>
	<mx:TextInput id="tags" width="100%"/>
	<mx:ControlBar>
		<mx:Button label="{resourceManager.getString('cms', 'saveChanges')}" click="save()" buttonMode="true"/>
	</mx:ControlBar>
</HelpTitleWindow>
