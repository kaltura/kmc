<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" verticalAlign="middle" horizontalAlign="left" paddingLeft="6">
	<mx:Script>
		<![CDATA[
			import com.kaltura.kmc.modules.content.events.EntryDistributionEvent;
			import com.kaltura.kmc.modules.content.model.EntryDistributionWithProfile;
			import com.kaltura.types.KalturaEntryDistributionFlag;
			import com.kaltura.types.KalturaEntryDistributionStatus;
			import com.kaltura.vo.KalturaEntryDistribution;
			
			private static const DISTRIBUTE:String = "distribute";
			private static const RETRY:String = "retry";
			private static const UPDATE:String = "update";
			
			private var _currentAction:String;
			private var _entryDis:KalturaEntryDistribution;
			
			override public function set data(value:Object):void {
				_entryDis = (value as EntryDistributionWithProfile).kalturaEntryDistribution;
				var action:String;

				if (_entryDis.validationErrors.length == 0) {
					switch (_entryDis.status) {
						case KalturaEntryDistributionStatus.PENDING:
							action = DISTRIBUTE;
							break;
						
						case KalturaEntryDistributionStatus.ERROR_DELETING:
						case KalturaEntryDistributionStatus.ERROR_SUBMITTING:
						case KalturaEntryDistributionStatus.ERROR_UPDATING:
							action = RETRY;
							break;
						case KalturaEntryDistributionStatus.READY:
							if (_entryDis.dirtyStatus==KalturaEntryDistributionFlag.UPDATE_REQUIRED)
								action = UPDATE;
							break;
						case KalturaEntryDistributionStatus.DELETING:
						case KalturaEntryDistributionStatus.SUBMITTING:
						case KalturaEntryDistributionStatus.UPDATING:
						case KalturaEntryDistributionStatus.QUEUED:
							break;		
					}
				}
				
				if (action) {
					actionBtn.visible = actionBtn.includeInLayout = true;
					actionBtn.label = resourceManager.getString('cms', action);
				}
				else {
					actionBtn.visible = actionBtn.includeInLayout = false;
				}
	
				_currentAction = action;
				
			}

			protected function actionBtn_clickHandler(event:MouseEvent):void
			{
				var actionEvent:EntryDistributionEvent;
				if (_currentAction == DISTRIBUTE) {
					actionEvent = new EntryDistributionEvent(EntryDistributionEvent.SUBMIT, null, null, _entryDis);
				}
				else if (_currentAction == RETRY) {
					actionEvent = new EntryDistributionEvent(EntryDistributionEvent.RETRY, null,null, _entryDis);
				}
				else if (_currentAction == UPDATE) {
					actionEvent = new EntryDistributionEvent(EntryDistributionEvent.UPDATE, null,null, _entryDis);
				}
				
				actionEvent.dispatch();
			}

		]]>
	</mx:Script>

	<mx:Button id="actionBtn" styleName="distributionButton" click="actionBtn_clickHandler(event)"/>
</mx:HBox>
