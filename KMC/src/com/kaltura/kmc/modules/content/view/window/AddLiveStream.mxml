<?xml version="1.0" encoding="utf-8"?>
<!---
	add new livestream window
-->
<HelpTitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:validators="com.kaltura.validators.*"
				 xmlns="com.kaltura.containers.*" layout="vertical" creationComplete="creationCompleteHandler(event)"
				 help="{onHelp()}" styleName="TitleWindowType2" width="500" height="420" showCloseButton="true"
				 close="removeMe()">
	<mx:Metadata>
		[ResourceBundle("live")]
	</mx:Metadata>

	<mx:Script>
		<![CDATA[
			import com.kaltura.edw.business.permissions.PermissionManager;
			import com.kaltura.edw.model.datapacks.PermissionsDataPack;
			import com.kaltura.edw.vo.LocalizedVo;
			import com.kaltura.kmc.events.KmcHelpEvent;
			import com.kaltura.kmc.modules.content.events.AddStreamEvent;
			import com.kaltura.kmc.modules.content.events.WindowEvent;
			import com.kaltura.kmc.modules.content.model.CmsModelLocator;
			import com.kaltura.kmc.modules.content.vo.StreamVo;
			import com.kaltura.types.KalturaRecordStatus;
			import com.kaltura.vo.KalturaConversionProfile;
			
			import mx.binding.utils.BindingUtils;
			import mx.binding.utils.ChangeWatcher;
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.events.ValidationResultEvent;
			import mx.resources.ResourceManager;

			private const labelWidth:Number = 180;
			

			[Bindable]
			/**
			 * value to show by default in ip fields
			 * */
			public var defaultIP:String;
			
			public function set model(value:CmsModelLocator):void {
				_model = value;
				_cw = BindingUtils.bindProperty(this, 'liveProfiles', value, 'liveConversionProfiles');
			}
			
			[Bindable]
			/**
			 * list of KalturaConversionProfiles where type = KalturaConversionProfileType.LIVE_STREAM
			 * */
			public var liveProfiles:ArrayCollection;
			
			/**
			 * live profiles load watcher
			 * */
			private var _cw:ChangeWatcher;
			
			[Bindable]
			/**
			 * for kaltura live stream - select recording by default
			 * */
			private var _recordByDefault:Boolean = true;
			
			private var _model:CmsModelLocator;

			[Bindable]
			/**
			 * options for live stream types
			 * */
			private var _streamTypes:Array;

			[Bindable]
			/**
			 * manual stream type selected
			 * */
			private var _isManualStream:Boolean = false;
			
			[Bindable]
			private var recordingOptions:ArrayCollection = new ArrayCollection();


			private function creationCompleteHandler(event:FlexEvent):void {
				title = resourceManager.getString('live', 'liveStreamTitle');
				PermissionManager.getInstance().applyAllAttributes(this, "createLiveStream");
				
				// now we can say if akamai/kaltura streams are available
				var pdp:PermissionsDataPack = _model.entryDetailsModel.getDataPack(PermissionsDataPack) as PermissionsDataPack;
				_streamTypes = [];
				
				if (pdp.enableKalturaLive) {
					_streamTypes.push(new LocalizedVo(StreamVo.STREAM_TYPE_KALTURA, 'kalturaStream', 'live'));
					if (!liveProfiles) {
						var lp:AddStreamEvent = new AddStreamEvent(AddStreamEvent.LIST_LIVE_PROFILES, null);
						lp.dispatch();
					}
				}
				
				if (pdp.enableKalturaMulticast) {
					_streamTypes.push(new LocalizedVo(StreamVo.STREAM_TYPE_MULTICAST, 'kalturaMulticast', 'live'));
					// live profiles load is handled by Kaltura live, which is a prerequisite for multicast
				}
				
				if (pdp.enableAkamaiLive) {
					_streamTypes.push(new LocalizedVo(StreamVo.STREAM_TYPE_UNIVERSAL, 'universalStream', 'live'));
				}
				_streamTypes.push(new LocalizedVo(StreamVo.STREAM_TYPE_MANUAL, 'manualStream', 'live'));
				
				streamType.selectedIndex = 0; // select the first item in the list
				streamType.dispatchEvent(new ListEvent(Event.CHANGE));
				
				streamTypeWrap.includeInLayout = streamTypeWrap.visible = pdp.enableAkamaiLive || pdp.enableKalturaLive;
				
				_recordByDefault = pdp.enableKalturaRecording;
				//build the dataprovider items list :
				recordingOptions.addItem(new LocalizedVo(String(KalturaRecordStatus.PER_SESSION), 'rec_slice', 'live'));
				recordingOptions.addItem(new LocalizedVo(String(KalturaRecordStatus.APPENDED), 'rec_append', 'live'));
				
			}


			private function onHelp():void {
				var anchor:String = 'section_add_live_stream'; // legacy rtmp
				switch ((streamType.selectedItem as LocalizedVo).value) {
					case StreamVo.STREAM_TYPE_UNIVERSAL:
						anchor = 'section_add_live_stream_ak';
						break;
					case StreamVo.STREAM_TYPE_MANUAL:
						anchor = 'section_add_live_stream_man';
						break;
					case StreamVo.STREAM_TYPE_KALTURA:
					case StreamVo.STREAM_TYPE_MULTICAST:
						anchor = 'section_add_live_stream_kal';
						break;
				}
				dispatchEvent(new KmcHelpEvent(KmcHelpEvent.HELP, anchor));
			}


			/**
			 * Remove current window command
			 */
			private function removeMe():void {
				var cgEvent:WindowEvent = new WindowEvent(WindowEvent.CLOSE);
				cgEvent.dispatch();
			}


			private function streamType_changeHandler(event:ListEvent):void {
				switch ((streamType.selectedItem as LocalizedVo).value) {
					case StreamVo.STREAM_TYPE_UNIVERSAL:
						_isManualStream = false;
						forms.selectedIndex = 1;
						break;
					case StreamVo.STREAM_TYPE_MANUAL:
						_isManualStream = true;
						forms.selectedIndex = 2;
						break;
					case StreamVo.STREAM_TYPE_KALTURA:
					case StreamVo.STREAM_TYPE_MULTICAST:
						_isManualStream = false;
						forms.selectedIndex = 0;
						break;

				}
			}


			/**
			 * When click on save - validate. if pass - save, if not - alert the user and activate the validators.
			 */
			private function onSave():void {
				// validate stream name
				var isValid:Boolean = streamNameValidator.validate().type == ValidationResultEvent.VALID;

				var streamVo:StreamVo = new StreamVo();
				streamVo.streamName = streamName.text;
				streamVo.description = streamDescription.text;
				
				if (streamTypeWrap.visible) {
					// options available
					streamVo.streamType = (streamType.selectedItem as LocalizedVo).value;
				} 
				else {
					// only "manual" stream available
					streamVo.streamType = StreamVo.STREAM_TYPE_MANUAL;
				}
				
				if (_isManualStream) { 
					// manual
					// make sure we have at least one url
					if (hdsurl.text == '' && hlsurl.text == '') {
						hdsurl.errorString = hlsurl.errorString = resourceManager.getString('live', 'at_least_one');
						isValid = false;
					}
					else {
						// make sure urls are valid
						hdsurlValidator.required = false;
						hdsurlValidator.listener = hdsurl;
						var isUrlValid:Boolean = hdsurlValidator.validate(hdsurl.text).type == ValidationResultEvent.VALID;
						hdsurlValidator.listener = hlsurl;
						isUrlValid &&= hdsurlValidator.validate(hlsurl.text).type == ValidationResultEvent.VALID;
						
						if (isUrlValid) {
							streamVo.flashHDSURL = hdsurl.text;
							streamVo.mobileHLSURL = hlsurl.text;
							streamVo.isAkamaiHds = isAkamaiHds.selected;
						}
						else {
							isValid = false;
						}
					}
				}
				else if (streamVo.streamType == StreamVo.STREAM_TYPE_KALTURA || streamVo.streamType == StreamVo.STREAM_TYPE_MULTICAST) {
					// kaltura
					var conversionProfileId:int = getLiveProfileId(liveProfileKaltura);
					
					if (conversionProfileId != 0) {
						if (streamVo.streamType == StreamVo.STREAM_TYPE_KALTURA) {
							streamVo.dvrEnabled = useDvrKaltura.selected;
						}
						else {
							// multicast doesn't support dvr
							streamVo.dvrEnabled = false;
						}
						streamVo.recordingEnabled = useRecKaltura.selected;
						streamVo.recordingType = recordingType.selectedItem.value;
						streamVo.conversionProfileId = conversionProfileId;
					} 
					else {
						isValid = false;
					}
				}
				else {
					// legacy / akamai
					// validate ips
					ipValidator.listener = streamPrimaryIp;
					var isIpValid:Boolean = ipValidator.validate(streamPrimaryIp.text).type == ValidationResultEvent.VALID;
					ipValidator.listener = streamSecondaryIp;
					isIpValid &&= ipValidator.validate(streamSecondaryIp.text).type == ValidationResultEvent.VALID;

					if (isIpValid) {
						streamVo.primaryIp = streamPrimaryIp.text;
						streamVo.secondaryIp = streamSecondaryIp.text;
						streamVo.password = streamBroadcastPassword.text;
					}
					else {
						isValid = false;
					}
					
					if (streamVo.streamType == StreamVo.STREAM_TYPE_UNIVERSAL) {
						streamVo.dvrEnabled = useDvr.selected;
					}
				}

				if (isValid) {
					// add stream
					var cgEv:AddStreamEvent = new AddStreamEvent(AddStreamEvent.ADD_STREAM, streamVo);
					cgEv.dispatch();
				}
				else {
					Alert.show(resourceManager.getString('live', 'fillAllMandatoryCorrect'));
				}
			}
			
			/**
			 * get the id of the selected live conversion profile
			 * */
			private function getLiveProfileId(combo:ComboBox):int
			{
				var ret:int = 0;
				if (combo.selectedItem) {
					ret = (combo.selectedItem as KalturaConversionProfile).id;
				}
				else if (liveProfiles && liveProfiles.length > 0) {
					ret = (liveProfiles[0] as KalturaConversionProfile).id;
				}
				
				return ret;
			}
			
		]]>
	</mx:Script>
	<mx:StringValidator id="streamNameValidator" property="text" source="{streamName}" 
						trigger="{saveButton}" triggerEvent="{MouseEvent.CLICK}" required="true"/>
	<validators:IPAddressValidator id="ipValidator" invalidIPError="{resourceManager.getString('live', 'invalidIP')}" />
	<validators:URLValidator id="hdsurlValidator" invalidURLError="{resourceManager.getString('live', 'invalidURL')}" />

	<mx:Spacer height="5" />	
	<mx:HBox width="100%" id="streamTypeWrap">
		<mx:Label text="{resourceManager.getString('live', 'streamType')}" width="{labelWidth}" styleName="boldLabel"/>
		<mx:ComboBox id="streamType" width="290" dataProvider="{_streamTypes}" change="streamType_changeHandler(event)"/>
	</mx:HBox>
	<mx:HRule width="100%" visible="{streamTypeWrap.visible}" includeInLayout="{streamTypeWrap.includeInLayout}" styleName="paddedHRule"/>

	<mx:HBox width="100%">
		<mx:Label text="{resourceManager.getString('live', 'name')}*" width="{labelWidth}" styleName="boldLabel"/>
		<mx:TextInput id="streamName" width="100%"/>
	</mx:HBox>
	<mx:HBox width="100%">
		<mx:Label text="{resourceManager.getString('live', 'liveDescription')}" width="{labelWidth}" styleName="boldLabel"/>
		<mx:TextInput id="streamDescription" width="100%"/>
	</mx:HBox>
	<mx:Spacer height="10" />
	<mx:ViewStack id="forms" width="100%" height="100%">
		<!-- form for kaltura and multicast -->
		<mx:VBox styleName="noPadding" width="100%" height="100%">
			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('live', 'liveProfile')}" width="{labelWidth}" styleName="boldLabel" />
				<mx:ComboBox id="liveProfileKaltura" dataProvider="{liveProfiles}" width="290" labelField="name" />
			</mx:HBox>
			<mx:HBox width="100%" visible="{streamType.selectedItem.value == StreamVo.STREAM_TYPE_KALTURA}"
					 includeInLayout="{streamType.selectedItem.value == StreamVo.STREAM_TYPE_KALTURA}">
				<mx:CheckBox id="useDvrKaltura" selected="false"/>
				<mx:Text text="{resourceManager.getString('live', 'enableDvrKaltura')}" width="100%" />
			</mx:HBox>
			<mx:HBox width="100%" >
				<mx:CheckBox id="useRecKaltura" selected="{_recordByDefault}" />
				<mx:Text text="{resourceManager.getString('live', 'enableRecording')}" width="100%" />
				<mx:ComboBox id="recordingType" enabled="{useRecKaltura.selected}" 
							 dataProvider="{recordingOptions}" width="290" />
			</mx:HBox>
		</mx:VBox>
		
		<!-- form for universal and legacy -->
		<mx:VBox styleName="noPadding" width="100%" height="100%">
			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('live', 'primaryIp')}*" width="{labelWidth}" styleName="boldLabel"/>
				<mx:TextInput id="streamPrimaryIp" width="100%" restrict="0-9\.\-\/" text="{defaultIP}"/>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('live', 'secondaryIp')}*" width="{labelWidth}" styleName="boldLabel"/>
				<mx:TextInput id="streamSecondaryIp" width="100%" restrict="0-9\.\-\/" text="{defaultIP}"/>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('live', 'ipdefault')}" width="100%" />
			</mx:HBox>
			<mx:Spacer height="10" />
			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('live', 'passwordOptional')}"
						  width="{labelWidth}" styleName="boldLabel"/>
				<mx:TextInput id="streamBroadcastPassword" width="100%"/>
			</mx:HBox>
			<mx:HBox width="100%" visible="{streamType.selectedItem.value == StreamVo.STREAM_TYPE_UNIVERSAL}"
					 includeInLayout="{streamType.selectedItem.value == StreamVo.STREAM_TYPE_UNIVERSAL}">
				<mx:CheckBox id="useDvr" selected="false"/>
				<mx:Text text="{resourceManager.getString('live', 'enableDvr')}" width="100%"/>
			</mx:HBox>
			
		</mx:VBox>

		<!-- form for manual -->
		<mx:VBox styleName="noPadding" width="100%" height="100%">
			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('live', 'hdsurl')}" width="{labelWidth}" styleName="boldLabel"/>
				<mx:TextInput id="hdsurl" width="100%" />
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:CheckBox id="isAkamaiHds" selected="true" label="{resourceManager.getString('live', 'is_akamai_hds')}" />
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="{resourceManager.getString('live', 'hlsurl')}" width="{labelWidth}" styleName="boldLabel"/>
				<mx:TextInput id="hlsurl" width="100%" />
			</mx:HBox>
		</mx:VBox>
		
		
	</mx:ViewStack>

	<mx:Label text="{resourceManager.getString('live', 'mandatory')}" width="100%"/>

	<mx:ControlBar>
		<mx:Button id="saveButton" label="{resourceManager.getString('live', 'createLiveBtn')}"
				   click="onSave()"/>
	</mx:ControlBar>

</HelpTitleWindow>
