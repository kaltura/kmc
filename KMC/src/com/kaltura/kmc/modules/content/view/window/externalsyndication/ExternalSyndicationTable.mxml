<?xml version="1.0" encoding="utf-8"?>
<!---
	 External syndications list in <code>ExternalSyndication</code>.
-->
<mx:DataGrid xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" allowMultipleSelection="true" selectable="true"
			 itemClick="onItemClick(event)" headerRelease="headerReleaseHandler(event)" rowHeight="30"
			 creationComplete="creationCompleteHandler(event)">
	<mx:Script>
		<![CDATA[
			import com.kaltura.dataStructures.HashMap;
			import com.kaltura.kmc.events.KmcHelpEvent;
			import com.kaltura.kmc.modules.content.events.ExternalSyndicationEvent;
			import com.kaltura.kmc.modules.content.model.CmsModelLocator;
			import com.kaltura.kmc.modules.content.view.window.externalsyndication.popupwindows.ExternalSyndicationPopUpWindow;
			import com.kaltura.kmc.modules.content.view.window.externalsyndication.renderers.ExternalSyndicationNameRenderer;
			import com.kaltura.kmc.modules.content.vo.ExternalSyndicationVO;
			import com.kaltura.types.KalturaBaseSyndicationFeedOrderBy;

			import mx.core.Application;
			import mx.events.DataGridEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;

			public static const ASCENDING:String = "ASC";
			public static const DESCENDING:String = "DESC";

			private var columnsSortMap:HashMap = new HashMap();

			/**
			 * the current sort index
			 * */
			private var _sortIndex:int = -1;

			/**
			 * the current sort direction
			 * */
			private var _sortDirection:String = DESCENDING;

			[Bindable]
			public var allSelected:Boolean = false;


			[Bindable]
			override public function set dataProvider(data:Object):void {
				super.dataProvider = data;
				setSortIndicator();
			}


			private function setSortIndicator():void {
				this.mx_internal::sortIndex = _sortIndex;
				this.mx_internal::sortDirection = _sortDirection;
			}


			private function headerReleaseHandler(event:DataGridEvent):void {
				event.preventDefault();
				if (event.itemRenderer && event.itemRenderer.data && event.itemRenderer.data.headerText) {
					var headerText:String = event.itemRenderer.data.headerText;
					if (!columnsSortMap.containsKey(headerText)) {
						columnsSortMap.put(headerText, false);
					}

					var sortDir:Boolean = columnsSortMap.getValue(headerText) as Boolean;
					var orederByStr:String;
					switch (headerText) {
						case resourceManager.getString('cms', 'exsFeedNameColumnHeader'):  {
							orederByStr = sortDir ? KalturaBaseSyndicationFeedOrderBy.NAME_ASC : KalturaBaseSyndicationFeedOrderBy.NAME_DESC;
							break;
						}

						case resourceManager.getString('cms', 'exsFeedTypeColumnHeader'):  {
							orederByStr = sortDir ? KalturaBaseSyndicationFeedOrderBy.TYPE_ASC : KalturaBaseSyndicationFeedOrderBy.TYPE_DESC;
							break;
						}

						case resourceManager.getString('cms', 'exsFeedcontentColumnHeader'):  {
							orederByStr = sortDir ? KalturaBaseSyndicationFeedOrderBy.PLAYLIST_ID_ASC : KalturaBaseSyndicationFeedOrderBy.PLAYLIST_ID_DESC;
							break;
						}

						case resourceManager.getString('cms', 'exsFeedURLColumnHeader'):  {
							orederByStr = sortDir ? KalturaBaseSyndicationFeedOrderBy.CREATED_AT_ASC : KalturaBaseSyndicationFeedOrderBy.CREATED_AT_DESC;
							break;
						}

					}
					// remember the direction and change the sign
					_sortIndex = event.columnIndex;
					_sortDirection = sortDir ? ASCENDING : DESCENDING;
					columnsSortMap.setValue(headerText, !sortDir);
					setSortIndicator();

					var cEvent:ExternalSyndicationEvent = new ExternalSyndicationEvent(ExternalSyndicationEvent.SET_SYNDICATION_FEED_FILTER_ORDER);
					cEvent.data = orederByStr;
					cEvent.dispatch();

					var getAllFeedsEvent:ExternalSyndicationEvent = new ExternalSyndicationEvent(ExternalSyndicationEvent.LIST_EXTERNAL_SYNDICATIONS);
					getAllFeedsEvent.dispatch();

				}

			}


			private function onItemClick(event:Event):void {
				var esVo:ExternalSyndicationVO;
				for each (esVo in this.dataProvider) {
					esVo.selected = false;
				}
				for each (esVo in this.selectedItems) {
					esVo.selected = true;
					esVo.dispatchEvent(new Event(ExternalSyndicationVO.SELECTED_CHANGED_EVENT));
				}
			}


			/**
			 * open edit syndication window
			 * */
			private function onClickProfileName(event:Event):void {
				var model:CmsModelLocator = CmsModelLocator.getInstance();
				var editSyndicationWin:ExternalSyndicationPopUpWindow = new ExternalSyndicationPopUpWindow();
				editSyndicationWin.flavorParams = model.filterModel.flavorParams;
				editSyndicationWin.extSynModel = model.extSynModel;
				editSyndicationWin.rootUrl = model.context.rootUrl;
				editSyndicationWin.selectedExSynVo = event.target.data as ExternalSyndicationVO;
				editSyndicationWin.addEventListener(KmcHelpEvent.HELP, dispatchHelp, false, 0, true);
				PopUpManager.addPopUp(editSyndicationWin, Application.application as DisplayObject, true);
				PopUpManager.centerPopUp(editSyndicationWin);
			}


			private function dispatchHelp(event:KmcHelpEvent):void {
				dispatchEvent(event.clone());
			}


			public function dispatchSelection(event:Event):void {
				var cbEvent:ExternalSyndicationEvent = new ExternalSyndicationEvent(ExternalSyndicationEvent.MARK_EXTERNAL_SYNDICATION,
					!allSelected);
				cbEvent.dispatch();
			}


			/**
			 * listen to item renderer events
			 * */
			protected function creationCompleteHandler(event:FlexEvent):void {
				addEventListener(ExternalSyndicationNameRenderer.PROFILE_NAME_CLICK, onClickProfileName, false, 0, true);
			}
		]]>
	</mx:Script>


	<mx:columns>
		<mx:DataGridColumn headerText="{resourceManager.getString('cms','exsFeedIdColumnHeader')}" 
						   dataField="id" width="80"
						   itemRenderer="com.kaltura.kmc.modules.content.view.window.externalsyndication.renderers.ExternalSyndicationIdRenderer"/>
		<mx:DataGridColumn headerText="{resourceManager.getString('cms','exsFeedNameColumnHeader')}" dataField="name"
						   itemRenderer="com.kaltura.kmc.modules.content.view.window.externalsyndication.renderers.ExternalSyndicationNameRenderer"/>
		<mx:DataGridColumn headerText="{resourceManager.getString('cms','exsFeedTypeColumnHeader')}" 
						   dataField="type" width="80"
						   itemRenderer="com.kaltura.kmc.modules.content.view.window.externalsyndication.renderers.ExternalSyndicationTypeRenderer"/>
		<mx:DataGridColumn headerText="{resourceManager.getString('cms','exsFeedContentColumnHeader')}"
						   dataField="content"
						   itemRenderer="com.kaltura.kmc.modules.content.view.window.externalsyndication.renderers.ExternalSyndicationContentRenderer"/>
		<mx:DataGridColumn headerText="{resourceManager.getString('cms','exsFeedURLColumnHeader')}" dataField="url"
						   itemRenderer="com.kaltura.kmc.modules.content.view.window.externalsyndication.renderers.ExternalSyndicationURLFeedRenderer"/>
	</mx:columns>
</mx:DataGrid>
