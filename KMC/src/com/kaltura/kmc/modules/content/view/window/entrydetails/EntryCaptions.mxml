<?xml version="1.0" encoding="utf-8"?>
<!---
Captions tab of EntryDetailsWin
-->
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%"
		 implements="com.kaltura.kmc.modules.content.business.IDrilldownPanel" creationComplete="onCreationComplete(event)">
	<mx:Script>
		<![CDATA[
			import com.kaltura.kmc.modules.content.events.CaptionsEvent;
			import com.kaltura.kmc.modules.content.view.window.entrydetails.captionsComponents.Caption;
			import com.kaltura.kmc.modules.content.vo.EntryCaptionVO;
			import com.kaltura.kmc.modules.content.vo.EntryDetailsValidationError;
			import com.kaltura.types.KalturaLanguage;
			import com.kaltura.types.KalturaNullableBoolean;
			import com.kaltura.vo.KalturaBaseEntry;
			import com.kaltura.vo.KalturaCaptionAsset;
			
			import mx.events.FlexEvent;
			
			public static const LANGUAGES_ARRAY:Array = new Array(
				KalturaLanguage.AB,
				KalturaLanguage.AA,
				KalturaLanguage.AF,
				KalturaLanguage.SQ,
				KalturaLanguage.AM,
				KalturaLanguage.AR,
				KalturaLanguage.HY,
				KalturaLanguage.AS_,
				KalturaLanguage.AY,
				KalturaLanguage.AZ,
				KalturaLanguage.BA,
				KalturaLanguage.EU,
				KalturaLanguage.BN,
				KalturaLanguage.DZ,
				KalturaLanguage.BH,
				KalturaLanguage.BI,
				KalturaLanguage.BR,
				KalturaLanguage.BG,
				KalturaLanguage.MY,
				KalturaLanguage.BE,
				KalturaLanguage.KM,
				KalturaLanguage.CA,
				KalturaLanguage.ZH,
				KalturaLanguage.CO,
				KalturaLanguage.HR,
				KalturaLanguage.CS,
				KalturaLanguage.DA,
				KalturaLanguage.NL,
				KalturaLanguage.EN,
				KalturaLanguage.EO,
				KalturaLanguage.ET,
				KalturaLanguage.FO,
				KalturaLanguage.FA,
				KalturaLanguage.FJ,
				KalturaLanguage.FI,
				KalturaLanguage.FR,
				KalturaLanguage.FY,
				KalturaLanguage.GL,
				KalturaLanguage.GD,
				KalturaLanguage.GV,
				KalturaLanguage.KA,
				KalturaLanguage.DE,
				KalturaLanguage.EL,
				KalturaLanguage.KL,
				KalturaLanguage.GN,
				KalturaLanguage.GU,
				KalturaLanguage.HA,
				KalturaLanguage.HE,
				KalturaLanguage.HI,
				KalturaLanguage.HU,
				KalturaLanguage.IS,
				KalturaLanguage.ID,
				KalturaLanguage.IA,
				KalturaLanguage.IE,
				KalturaLanguage.IU,
				KalturaLanguage.IK,
				KalturaLanguage.GA,
				KalturaLanguage.IT,
				KalturaLanguage.JA,
				KalturaLanguage.JV,
				KalturaLanguage.KN,
				KalturaLanguage.KS,
				KalturaLanguage.KK,
				KalturaLanguage.RW,
				KalturaLanguage.KY,
				KalturaLanguage.RN,
				KalturaLanguage.KO,
				KalturaLanguage.KU,
				KalturaLanguage.LO,
				KalturaLanguage.LA,
				KalturaLanguage.LV,
				KalturaLanguage.LI,
				KalturaLanguage.LN,
				KalturaLanguage.LT,
				KalturaLanguage.MK,
				KalturaLanguage.MG,
				KalturaLanguage.MS,
				KalturaLanguage.ML,
				KalturaLanguage.MT,
				KalturaLanguage.MI,
				KalturaLanguage.MR,
				KalturaLanguage.MO,
				KalturaLanguage.MN,
				KalturaLanguage.NA,
				KalturaLanguage.NE,
				KalturaLanguage.NO,
				KalturaLanguage.OC,
				KalturaLanguage.OR_,
				KalturaLanguage.OM,
				KalturaLanguage.PS,
				KalturaLanguage.PL,
				KalturaLanguage.PT,
				KalturaLanguage.PA,
				KalturaLanguage.QU,
				KalturaLanguage.RM,
				KalturaLanguage.RO,
				KalturaLanguage.RU,
				KalturaLanguage.SM,
				KalturaLanguage.SG,
				KalturaLanguage.SA,
				KalturaLanguage.SR,
				KalturaLanguage.SH,
				KalturaLanguage.ST,
				KalturaLanguage.TN,
				KalturaLanguage.SN,
				KalturaLanguage.SD,
				KalturaLanguage.SI,
				KalturaLanguage.SS,
				KalturaLanguage.SK,
				KalturaLanguage.SL,
				KalturaLanguage.SO,
				KalturaLanguage.ES,
				KalturaLanguage.SU,
				KalturaLanguage.SW,
				KalturaLanguage.SV,
				KalturaLanguage.TL,
				KalturaLanguage.TG,
				KalturaLanguage.TA,
				KalturaLanguage.TT,
				KalturaLanguage.TE,
				KalturaLanguage.TH,
				KalturaLanguage.BO,
				KalturaLanguage.TI,
				KalturaLanguage.TO,
				KalturaLanguage.TS,
				KalturaLanguage.TR,
				KalturaLanguage.TK,
				KalturaLanguage.TW,
				KalturaLanguage.UG,
				KalturaLanguage.UK,
				KalturaLanguage.UR,
				KalturaLanguage.UZ,
				KalturaLanguage.VI,
				KalturaLanguage.VO,
				KalturaLanguage.CY,
				KalturaLanguage.WO,
				KalturaLanguage.XH,
				KalturaLanguage.YI,
				KalturaLanguage.YO,
				KalturaLanguage.ZU);
			private var _selectedEntry:KalturaBaseEntry;
			private var _entryCaptionsArr:Array;
			private var _captionsToRemove:Array;
			private var _defaultCaption:Caption;
			
			[Bindable]
			/**
			 * for r&p
			 * */
			public var editable:Boolean = true;
			
			public function get entryCaptionsArr():Array
			{
				return _entryCaptionsArr;
			}

			/**
			 * The current entry available captions
			 * */
			public function set entryCaptionsArr(value:Array):void
			{
				_entryCaptionsArr = value;
				populateCaptions();
			}

			public function get selectedEntry():KalturaBaseEntry
			{
				return _selectedEntry;
			}

			/**
			 * The current drilled down entry
			 * */
			public function set selectedEntry(value:KalturaBaseEntry):void
			{
				_selectedEntry = value;
			}

			/**
			 * initialize required data
			 * */
			public function initData():void {
				var listCaptionsEvt:CaptionsEvent = new CaptionsEvent(CaptionsEvent.LIST_CAPTIONS);
				listCaptionsEvt.dispatch();	
			}
			
			/**
			 * create a Caption object with given captionVO or a new EntryCaptionVO
			 * */
			private function createCaption(captionVO:EntryCaptionVO = null):Caption {
				var newCaption:Caption = new Caption();
				if (!captionVO) {
					captionVO = new EntryCaptionVO();
					captionVO.caption = new KalturaCaptionAsset();		
				}
				else {
					if (captionVO.caption.isDefault==KalturaNullableBoolean.TRUE_VALUE) {
						newCaption.isDefault = true;
						_defaultCaption = newCaption;
					}
				}
				newCaption.caption = captionVO;
				newCaption.editable = editable;
				newCaption.addEventListener(Caption.REMOVE_CAPTION, removeCaption);
				newCaption.addEventListener(Caption.CHANGE_DEFAULT, changeDefaultCaption);
				return newCaption;
			}
			
			private function changeDefaultCaption(event:Event):void {
				var caption:Caption = event.target as Caption;
				caption.isDefault = true;
				if (_defaultCaption)
					_defaultCaption.isDefault = false;
				_defaultCaption = caption;
			}
			
			/**
			 * When a caption is removed- remove from display list and remember it for save
			 * */
			private function removeCaption(event:Event):void {
				for (var i:int = 0 ; i<captions.numChildren; i++) {
					if (captions.getChildAt(i) == event.target) {
						captions.removeChildAt(i);
						if ((event.target as Caption).caption.caption.id)
							_captionsToRemove.push((event.target as Caption).caption);
					}
				}
			}
			
			/**
			 * load captions to the form
			 * */
			private function populateCaptions():void {
				if (!_entryCaptionsArr || !captions)
					return;
				_captionsToRemove = new Array();
				captions.removeAllChildren();
				if (_entryCaptionsArr.length == 0) {
					captions.addChild(createCaption());
				}
				else {
					for (var i:int = 0; i<_entryCaptionsArr.length; i++) {
						captions.addChild(createCaption(_entryCaptionsArr[i] as EntryCaptionVO));
					}
				}
			}

			private function onCreationComplete(event:FlexEvent):void
			{
				populateCaptions();
			}
			
			public function destroy():void {
				
			}
			
			/**
			 * add a new empty Caption to captions VBox
			 * */
			private function addCaption():void {
				captions.addChild(createCaption());
			}
			
			/**
			 * save all changes- added/removed captions
			 * */
			public function save():EntryDetailsValidationError {
				var result:EntryDetailsValidationError = new EntryDetailsValidationError();
				var captionsToSave:Array = new Array();
				for (var i:int = 0; i<captions.numChildren; i++) {
					var curCaption:Caption = captions.getChildAt(i) as Caption;
					if (curCaption.wasChanged()) {
						var curCaptionRes:EntryDetailsValidationError = curCaption.save();
						if (curCaptionRes.error) {
							result = curCaptionRes;
						}
						else {
							captionsToSave.push(curCaption.caption);
						}
					}
				}
				
				if (!result.error)
				{
					var saveEvent:CaptionsEvent = new CaptionsEvent(CaptionsEvent.SAVE_ALL);
					saveEvent.captionsToSave = captionsToSave;
					saveEvent.captionsToRemove = _captionsToRemove;
					if (_defaultCaption)
						saveEvent.defaultCaption = _defaultCaption.caption;
					saveEvent.dispatch();
				}
				
				return result;
			}
			
			/**
			 * return true if there are changes to save
			 * */
			public function wasChanged():Boolean {
				if (_captionsToRemove.length)
					return true;
				
				for (var i:int =0; i<captions.numChildren; i++) {
					var curCaption:Caption = captions.getChildAt(i) as Caption;
					if (curCaption.wasChanged() || (curCaption.isDefault!= (curCaption.caption.caption.isDefault==KalturaNullableBoolean.TRUE_VALUE)))
						return true;
				}
				
				return false;
			}

		]]>
	</mx:Script>
	<mx:Text text="{resourceManager.getString('cms','captionsInfo')}" styleName="tipText"/>
	<mx:VBox id="captions" paddingTop="10" paddingBottom="10"/>
	<mx:Button label="{resourceManager.getString('cms','addCaptions')}" click="addCaption()" visible="{editable}"/>
</mx:VBox>
