<?xml version="1.0" encoding="utf-8"?>
<!---
	 This panel allows changing access control for entries
-->
<HelpTitleWindow xmlns="com.kaltura.containers.*" xmlns:mx="http://www.adobe.com/2006/mxml" 
				 title="{resourceManager.getString('cms', 'setAccessControlTitle')}" layout="vertical"
				 showCloseButton="true" close="closeWindow(event)" help="onHelp(event)" 
				 xmlns:view="com.kaltura.edw.view.*">
	<mx:Script>
		<![CDATA[
			import com.kaltura.analytics.GoogleAnalyticsConsts;
			import com.kaltura.analytics.GoogleAnalyticsTracker;
			import com.kaltura.analytics.KAnalyticsTracker;
			import com.kaltura.analytics.KAnalyticsTrackerConsts;
			import com.kaltura.kmc.events.KmcHelpEvent;
			import com.kaltura.edw.business.Cloner;
			import com.kaltura.edw.control.events.EntriesEvent;
			import com.kaltura.edw.control.events.WindowEvent;
			import com.kaltura.edw.model.FilterModel;
			import com.kaltura.types.KalturaStatsKmcEventType;
			import com.kaltura.vo.AccessControlProfileVO;
			import com.kaltura.vo.KalturaBaseEntry;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;


			private var _selectedEntries:ArrayCollection;
			
			[Bindable]
			private var _clonedSelectedEntries:ArrayCollection = new ArrayCollection();

			[Bindable]
			public var filterModel:FilterModel;
			
			
			public function set selectedEntries(arr:ArrayCollection):void {
				_selectedEntries = arr;
				var tempArrCol:ArrayCollection = new ArrayCollection();
				for (var i:int = 0; i < _selectedEntries.length; i++) {
					tempArrCol.addItem(Cloner.cloneKalturaBaseEntry(_selectedEntries[i]));
				}

				_clonedSelectedEntries = tempArrCol;
			}


			private function closeWindow(event:Event = null):void {
				var cgEvent:WindowEvent = new WindowEvent(WindowEvent.CLOSE);
				cgEvent.dispatch();
			}


			private function saveData(event:Event):void {
				var seletedAccessControl:Object = entryACP.getSelectedAccessControlProfile();
				if (seletedAccessControl == null) {
					Alert.show(resourceManager.getString('cms', 'applyAccessControlAlert'));
					return;
				}
				for each (var entry:KalturaBaseEntry in _clonedSelectedEntries) {
					entry.accessControlId = (seletedAccessControl as AccessControlProfileVO).profile.id;
				}
				var cgEvent:EntriesEvent = new EntriesEvent(EntriesEvent.UPDATE_ENTRIES,
															_clonedSelectedEntries, false);
				cgEvent.dispatch();

				for each (var etr:Object in _clonedSelectedEntries) {
					KAnalyticsTracker.getInstance().sendEvent(KAnalyticsTrackerConsts.CONTENT,KalturaStatsKmcEventType.CONTENT_EDIT_ENTRY,
															  "setAccessControlProfileIdForEntries", etr.id);

				}
				GoogleAnalyticsTracker.getInstance().sendToGA(GoogleAnalyticsConsts.CONTENT_ACCESS_CONTROL,GoogleAnalyticsConsts.CONTENT);
				closeWindow();
			}

			protected function onHelp(event:Event):void {
				dispatchEvent(new KmcHelpEvent(KmcHelpEvent.HELP, 'section23'));
			}

		]]>
	</mx:Script>
	<mx:Label text="{resourceManager.getString('cms', 'accessControlOverrideMsg')}"/>
	<view:EntryAccessControl id="entryACP" selectedEntries="{_clonedSelectedEntries}"
				  filterModel="{filterModel}" accessControlData="{filterModel.accessControlProfiles}"/>
	<mx:ControlBar width="100%" horizontalAlign="center" paddingBottom="2" paddingLeft="2" paddingRight="2"
				   paddingTop="2">
		<mx:Button label="{resourceManager.getString('cms', 'applyAccessControlBtn')}" useHandCursor="true"
				   buttonMode="true" click="saveData(event)"/>
	</mx:ControlBar>
</HelpTitleWindow>
