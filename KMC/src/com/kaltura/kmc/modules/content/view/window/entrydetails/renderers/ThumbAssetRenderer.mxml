<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" horizontalAlign="center" verticalAlign="middle">
	<mx:Script>
		<![CDATA[
			import com.kaltura.kmc.modules.content.model.ThumbnailWithDimensions;
			import com.kaltura.types.KalturaFlavorAssetStatus;
			
			import mx.controls.Image;
			
			[Embed(source="assets/flash/loaderwhite.swf")]
			private static const progressImage:Class;
			[Embed(source="assets/images/thumb_error.png")]
			private static const errorImage:Class;
			
			public static const DEFAULT_THUMB:String = "default_thumb";
			public static const THUMB_WIDTH:int = 90;
			public static const THUMB_HEIGHT:int = 90;
			
			[Bindable]
			private var thumbAssetExist:Boolean = false;
			[Bindable]
			private var isDefault:Boolean = false;
			

			override public function set data(value:Object):void {
				var thumb:ThumbnailWithDimensions = value as ThumbnailWithDimensions;
				if (thumb.thumbAsset) {
					thumbAssetExist = true;
					if ((thumb.thumbAsset.status == KalturaFlavorAssetStatus.CONVERTING) ||
						(thumb.thumbAsset.status == KalturaFlavorAssetStatus.QUEUED)) 
					{
						setImageAndDimensions(progressImage, THUMB_WIDTH/2, THUMB_HEIGHT/2);
					}
					else if (thumb.thumbAsset.status == KalturaFlavorAssetStatus.ERROR) {
						setImageAndDimensions(errorImage, THUMB_WIDTH/2, THUMB_HEIGHT/2);
					}
					else
					{
						setImageAndDimensions(thumb.thumbUrl, THUMB_WIDTH, THUMB_HEIGHT);
				
						if (thumb.thumbAsset.tags == DEFAULT_THUMB)
							isDefault = true;
						else
							isDefault = false;	
					}			
				}
				else {
					thumbAssetExist = false;
					thumbImage.source = null;
					isDefault = false;
				}
			}
			
			private function setImageAndDimensions(source:Object, width:int, height:int):void {
				thumbImage.source = source;
				thumbImage.width = width;
				thumbImage.height = height;
			}
		]]>
	</mx:Script>
	
	<mx:VBox visible="{!thumbAssetExist}" includeInLayout="{!thumbAssetExist}" width="{THUMB_WIDTH}" height="{THUMB_HEIGHT}" verticalAlign="middle" horizontalAlign="center">
		<mx:Label text="{resourceManager.getString('cms', 'noThumbnails')}" fontWeight="bold"/>	
	</mx:VBox>
	<mx:HBox verticalAlign="middle" width="{THUMB_WIDTH+60}" height="{THUMB_WIDTH}" horizontalAlign="center">	
		<mx:Label text="default"  visible="{isDefault}" includeInLayout="{isDefault}"/>
		<mx:Image id="thumbImage" visible="{thumbAssetExist}" includeInLayout="{thumbAssetExist}" width="{THUMB_WIDTH}" height="{THUMB_WIDTH}"/>
	</mx:HBox>
	
</mx:HBox>
