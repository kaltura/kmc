<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" 
		 implements="com.kaltura.kmc.modules.content.view.window.cdw.ICategoryDrilldownPanel"
		 creationComplete="creationCompleteHandler(event)">
	<mx:Script>
		<![CDATA[
			import com.adobe.cairngorm.control.CairngormEvent;
			import com.kaltura.kmc.modules.content.events.CategoryEvent;
			import com.kaltura.kmc.modules.content.model.CategoriesModel;
			import com.kaltura.kmc.modules.content.view.window.cdw.ICategoryDrilldownPanel;
			import com.kaltura.kmc.modules.content.view.window.cdw.components.categoryTabs.renderers.CategoryPositionRenderer;
			import com.kaltura.vo.KalturaCategory;
			
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			
			
			[Bindable]
			private var _categories:ArrayCollection;
			
			/**
			 * list of categories for reordering
			 * */
			public function set categories(value:ArrayCollection):void {
				_categories = value;
				if (value) {
					if (value.length == 0 || value.length > CategoriesModel.SUB_CATEGORIES_LIMIT) {
						if (this.parent) {
							this.parent.removeChild(this);
						}
					}
					else {
						setFirstLast();
					}
				}
			}
			
			public function get categories():ArrayCollection {
				return _categories;
			}
			
			private function creationCompleteHandler(event:FlexEvent):void {
				addEventListener(CategoryPositionRenderer.MOVE_DOWN, handleMove, false, 0, true);
				addEventListener(CategoryPositionRenderer.MOVE_UP, handleMove, false, 0, true);
			}
			
			
			/**
			 * tell the head and tail of the list they are head and tail
			 * */
			private function setFirstLast():void {
				_categories.getItemAt(0).isFirst = true;
				_categories.getItemAt(_categories.length-1).isLast = true;
			}
			
			
			/**
			 * reorder categories according to event. <br/>
			 * use 2 dynamic props to handle moving: <br/>
			 * <code>isFirst/isLast</code> to decide if up/down buttons should be shown 
			 * */
			private function handleMove(e:Event):void {
				var cat:KalturaCategory = e.target.data as KalturaCategory;
				var ind:int = _categories.getItemIndex(cat);
				
				if (e.type == CategoryPositionRenderer.MOVE_DOWN) {
					if (ind == _categories.length - 2) {
						_categories.getItemAt(_categories.length - 1).isLast = false;
					}
					else if (ind == 0) {
						cat.isFirst = false;
					}
					_categories.removeItemAt(ind);
					_categories.addItemAt(cat, ind + 1);
				}
				else if (e.type == CategoryPositionRenderer.MOVE_UP) {
					if (ind == 1) {
						_categories.getItemAt(0).isFirst = false;
					}
					else if (ind == _categories.length-1) {
						cat.isLast = false;
					}
					_categories.removeItemAt(ind);
					_categories.addItemAt(cat, ind - 1);
				}
				setFirstLast();
			}
			
			
			public function validate():void {
				
			}
			
			public function save():void {
				var updated:Array = [];
				var cat:KalturaCategory;
				for (var i:int = 0; i<_categories.length; i++) {
					cat = _categories.getItemAt(i) as KalturaCategory;
					if (cat.partnerSortValue != i) {
						cat.partnerSortValue = i;
						updated.push(cat);
					}
				}
				if (updated.length > 0) {
					var cgEvent:CairngormEvent = new CategoryEvent(CategoryEvent.UPDATE_SUB_CATEGORIES);
					cgEvent.data = updated;
					cgEvent.dispatch();
				}
			}
			
			public function destroy():void {
			
			}
			
			public function clear():void {
				var cgEvent:CairngormEvent = new CategoryEvent(CategoryEvent.RESET_SUB_CATEGORIES);
				cgEvent.dispatch();
			}
			
			public function initData():void {
				var cgEvent:CairngormEvent = new CategoryEvent(CategoryEvent.GET_SUB_CATEGORIES);
				cgEvent.dispatch();
			}
			
			public function init():void {
			
			}
			
			public function isChanged():Boolean {
				var cat:KalturaCategory;
				for (var i:int = 0; i<_categories.length; i++) {
					cat = _categories.getItemAt(i) as KalturaCategory;
					if (cat.partnerSortValue != i) {
						return true;
					}
				}
				return false;
			}


		]]>
	</mx:Script>
	
	<mx:Label text="{resourceManager.getString('cms', 'subcatsDescription')}" />
	<mx:DataGrid id="catsList" dataProvider="{_categories}" width="100%" height="100%">
		<mx:columns>
			<mx:DataGridColumn id="catName" dataField="name" 
							   headerText="{resourceManager.getString('cms', 'subcat')}"/>
			<mx:DataGridColumn id="catPosition" width="120"
							   headerText="{resourceManager.getString('cms', 'subcatsPosition')}"
				itemRenderer="com.kaltura.kmc.modules.content.view.window.cdw.components.categoryTabs.renderers.CategoryPositionRenderer"/>
		</mx:columns>
	</mx:DataGrid>
	
	<mx:Button click="clear()"  label="clear"/>
	<mx:Button click="save()"  label="save"/>
</mx:VBox>
