<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" horizontalAlign="center" verticalAlign="middle"
		 horizontalScrollPolicy="off">
	<mx:Script>
		<![CDATA[
			import com.kaltura.kmc.modules.content.model.ThumbnailWithDimensions;
			import com.kaltura.types.KalturaFlavorAssetStatus;
			
			import mx.controls.Image;
			
			[Embed(source="assets/flash/loaderwhite.swf")]
			private static const progressImage:Class;
			[Embed(source="assets/images/thumb_error.png")]
			private static const errorImage:Class;
			[Embed(source="assets/images/ico_yes.png")]
			private static const defaultIcon:Class;
			
			public static const THUMB_WIDTH:int = 90;
			public static const THUMB_HEIGHT:int = 90;
			
			[Bindable]
			private var thumbAssetExist:Boolean = false;
			[Bindable]
			private var isDefault:Boolean = false;
			
			
			override public function set data(value:Object):void {
				var thumb:ThumbnailWithDimensions = value as ThumbnailWithDimensions;
				if (thumb.thumbAsset) {
					thumbAssetExist = true;
					if ((thumb.thumbAsset.status == KalturaFlavorAssetStatus.CONVERTING) ||
						(thumb.thumbAsset.status == KalturaFlavorAssetStatus.QUEUED)) 
					{
						setImageAndDimensions(progressImage, THUMB_WIDTH/2, THUMB_HEIGHT/2);
					}
					else if (thumb.thumbAsset.status == KalturaFlavorAssetStatus.ERROR) {
						setImageAndDimensions(errorImage, THUMB_WIDTH/2, THUMB_HEIGHT/2);
					}
					else
					{
						setImageAndDimensions(thumb.thumbUrl, THUMB_WIDTH, THUMB_HEIGHT);	
					}
				
					isDefault = getIsDefault(thumb);
					
				}
				else {
					thumbAssetExist = false;
					thumbImage.source = null;
					isDefault = false;
				}
			}
			
			private function getIsDefault(thumb:ThumbnailWithDimensions):Boolean {
				var result:Boolean = false;
				var ar:Array = thumb.thumbAsset.tags.split(",");
				for each (var tag:String in ar){
					if (tag == ThumbnailWithDimensions.DEFAULT_THUMB) {
						result = true;
						break;
					} 
				}
				return result;
				
			}
			
			private function setImageAndDimensions(source:Object, width:int, height:int):void {
				thumbImage.source = source;
				thumbImage.width = width;
				thumbImage.height = height;
			}
		]]>
	</mx:Script>

	<mx:TextArea backgroundAlpha="0" borderStyle="none" visible="{!thumbAssetExist}" includeInLayout="{!thumbAssetExist}" text="{resourceManager.getString('cms', 'noThumbnails')}" 
				 fontWeight="bold" width="100%" textAlign="center" editable="false"/>
	<mx:HBox verticalAlign="middle" horizontalAlign="center" visible="{thumbAssetExist}" includeInLayout="{thumbAssetExist}" >
		<mx:Spacer width="{defaultImg.width}"/>
		<mx:Image id="thumbImage" horizontalAlign="center" verticalAlign="middle" width="{THUMB_WIDTH}" height="{THUMB_WIDTH}"/>
		<mx:Image id="defaultImg" source="{defaultIcon}" visible="{isDefault}" includeInLayout="{thumbAssetExist}"/>
	</mx:HBox>
	
</mx:HBox>
