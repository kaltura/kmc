<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" implements="com.kaltura.edw.business.IDrilldownPanel"
		 show="onShow()" verticalGap="{customDataVerticalGap}" >
	<mx:Script>
		<![CDATA[
			import com.kaltura.edw.control.events.MetadataDataEvent;
			import com.kaltura.edw.events.ScrollToProfileEvent;
			import com.kaltura.edw.business.FormBuilder;
			import com.kaltura.edw.view.customData.SingleCustomData;
			import com.kaltura.utils.pager.Pager;
			import com.kaltura.vo.KMCMetadataProfileVO;
			
			import flash.sampler.getInvocationCount;
			
			import mx.collections.ArrayCollection;
			import mx.controls.HRule;
			import mx.events.ListEvent;
			
			private const customDataVerticalGap:Number = 5;
			
			public var selectedProfileIndex:int;
			[Bindable]
			private var _pager:Pager;
			[Bindable]
			private var _profilesAC:ArrayCollection;
			
			public function destroy():void {
				for (var i:int = 0; i<profilesVBox.numChildren; i++) {
					(profilesVBox.getChildAt(i) as SingleCustomData).destroy();
				}
			}
			
			/**
			 * loads required data from server
			 * */
			public function initData():void {
				var listCustomData:MetadataDataEvent = new MetadataDataEvent(MetadataDataEvent.LIST)
				listCustomData.dispatch();
			}
			
			public function buildProfiles(profilesAC:ArrayCollection, formBuildersAC:ArrayCollection):void {
				_profilesAC = new ArrayCollection();
				//remove old data and old binding
				destroy();
				profilesVBox.removeAllChildren();
						
				for (var i:int = 0; i<profilesAC.length; i++) {					
					var curProfile:KMCMetadataProfileVO = profilesAC.getItemAt(i) as KMCMetadataProfileVO;
					
					if (curProfile.profile && curProfile.metadataFieldVOArray && curProfile.metadataFieldVOArray.length>0) {
						var curCustomData:SingleCustomData= new SingleCustomData();	
						var curFormBuilder:FormBuilder = formBuildersAC.getItemAt(i) as FormBuilder;
						curCustomData.formBuilder = curFormBuilder;
						curCustomData.addEventListener(SingleCustomData.SCROLL_TO_TOP, scrollToTop, false, 0, true);

						_profilesAC.addItem(curProfile.profile.name);
						profilesVBox.addChild(curCustomData);			
					}
				}
				
				profilesVBox.verticalScrollPosition = 0;
			}
			
			private function scrollToTop(event:Event):void {
				profilesVBox.verticalScrollPosition  = 0;
			}
			
			/**
			 * page to the previous page
			 * */
			private function onPrevClick():void {
				_pager.prevPage();
			}
			
			/***
			 * pages to the next page
			 * */
			private function onNextClick():void {
				_pager.nextPage();
			}
			
			private function onShow():void {
				for (var i:int = 0; i < profilesVBox.numChildren; i++) {
					if (profilesVBox.getChildAt(i) is SingleCustomData)
						(profilesVBox.getChildAt(i) as SingleCustomData).onShow();
				}
			}	

			protected function jumpToProfile():void
			{
				var scrollPos:int = 0;
				var childIndex:int = profilesCB.selectedIndex;
				if (childIndex!=-1) {
					//sums up all heights before the desired profile
					for (var i:int = 0; i< childIndex; i++) {
						scrollPos += profilesVBox.getChildAt(i).height + customDataVerticalGap;
					}
					profilesVBox.verticalScrollPosition = scrollPos;
				}
				profilesCB.selectedIndex = -1;
			}

		]]>
	</mx:Script>
	<mx:HBox id="navigationHbox" verticalAlign="middle">	
		<mx:ComboBox id="profilesCB" dataProvider="{_profilesAC}" change="jumpToProfile()" prompt="{resourceManager.getString('drilldown','jumpToPrompt')}" 
					 selectedIndex="-1" fontWeight="bold" visible="{_profilesAC.length > 1}" includeInLayout="{_profilesAC.length > 1}"/>
	</mx:HBox>
	<mx:VBox id="profilesVBox" verticalGap="{customDataVerticalGap}" height="{this.height - navigationHbox.height - customDataVerticalGap}"
			 styleName="profilesVBox" width="100%"/>
</mx:VBox>
