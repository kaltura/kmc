<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" implements="com.kaltura.edw.business.IDrilldownPanel"
		 width="100%" height="100%" xmlns:view="com.kaltura.edw.view.*" xmlns:controls="com.kaltura.controls.*" xmlns:et="com.kaltura.edw.view.et.*" >
	<mx:Script>
		<![CDATA[
			import com.kaltura.edw.control.events.ClipEvent;
			import com.kaltura.vo.KalturaBaseEntry;
			
			import mx.binding.utils.BindingUtils;
			import mx.binding.utils.ChangeWatcher;
			import mx.events.FlexEvent;

			/**
			 * current selecred entry
			 * */
			public var selectedEntry:KalturaBaseEntry;

			[Bindable]
			/**
			 * vos for the clips to show in the table
			 * */
			public var clips:Array;
			
			/**
			 * to watch when <code>clips</code> change
			 * */
			private var _cw:ChangeWatcher;
			

			public function initData():void {
				// reset previous data
				var ce:ClipEvent = new ClipEvent(ClipEvent.RESET_MODEL_ENTRY_CLIPS);
				ce.dispatch();
				// monitor data change if needed
				if (!_cw) {
					_cw = BindingUtils.bindSetter(removeTab, this, "clips");
				}
				// get data
				getNext();
			}

			private function getNext():void {
				var gc:ClipEvent = new ClipEvent(ClipEvent.GET_ENTRY_CLIPS);
				gc.data = {id: selectedEntry.id};
				if (paging) {
					gc.data.pager = paging.kalturaFilterPager;
				}
				gc.dispatch();
			}

			public function destroy():void {
				_cw.unwatch();
			}


			private function removeColumns(event:FlexEvent):void {
				entryTable.removeColumn(resourceManager.getString('drilldown', 'thumbnail'));
//				entryTable.removeColumn(resourceManager.getString('drilldown', 'idHeader'));
//				entryTable.removeColumn(resourceManager.getString('drilldown', 'name'));
				entryTable.removeColumn(resourceManager.getString('drilldown', 'playlistType'));
				entryTable.removeColumn(resourceManager.getString('drilldown', 'type'));
				entryTable.removeColumn(resourceManager.getString('drilldown', 'playerLoads'));
//				entryTable.removeColumn(resourceManager.getString('drilldown', 'plays'));
				entryTable.removeColumn(resourceManager.getString('drilldown', 'rating'));
//				entryTable.removeColumn(resourceManager.getString('drilldown', 'creator'));
//				entryTable.removeColumn(resourceManager.getString('drilldown', 'createAt'));
//				entryTable.removeColumn(resourceManager.getString('drilldown', 'duration'));
				entryTable.removeColumn(resourceManager.getString('drilldown', 'flags'));
				entryTable.removeColumn(resourceManager.getString('drilldown', 'moderationStatus'));
				entryTable.removeColumn(resourceManager.getString('drilldown', 'preview'));

			}

			/**
			 * if entry has no clips, remove the tab.
			 * @internal
			 * when destroying the drilldown we reset the value on the model, 
			 * so when the binding is first fired on creation the condition falls
			 * on null value and the tab is not removed even if the previous 
			 * entry did not have clips. 
			 * */
			private function removeTab(value:Array):void {
				if (value && value.length == 0 && this.parent) {
					this.parent.removeChild(this);
				}
			}

		]]>
	</mx:Script>
	<mx:VBox styleName="entriesTablePagerAndButtonsVbox" width="100%" height="100%" >
		<et:EntryTable id="entryTable" width="100%" height="100%" kalturaSorting="false" showLinks="false" 
						 dataProvider="{clips}" creationComplete="removeColumns(event)"/>
		
		<controls:Paging id="paging" width="100%" styleName="paging"
						 rowsInPageChange="getNext()" nextPage="getNext()" prvPage="getNext()"
						 getPageNum="getNext()" totalCount="{clips.length}"
						 showRowsInPage="true" defaultPageSize="10" />
	</mx:VBox>
</mx:VBox>
