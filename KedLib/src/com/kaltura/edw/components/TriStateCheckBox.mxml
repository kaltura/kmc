<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml">
	<mx:Metadata>
		[Event(name="change", type="flash.events.Event")] 
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			
			public static var UNSELECTED:int = 0;
			public static var PARTIAL:int = 1;
			public static var SELECTED:int = 2;
			
			
			

			override public function get label():String {
				return box.label;
			}

			override public function set label(value:String):void {
				super.label = value;
				box.label = value;
			}

			
			/**
			 * name of the style to use for complete selection
			 * */
			public var selectedStyleName:String = "adminMainCheckbox";
			
			/**
			 * name of style to use for partial selection
			 * */
			public var partialStyleName:String = "partial";
			
			
			private var _selected:int;

			public function get selected():int
			{
				return _selected;
			}

			public function set selected(value:int):void
			{
				_selected = value;
				switch (value){
					case UNSELECTED:
						box.selected = false;
						box.styleName = selectedStyleName;
						break;
					case PARTIAL:
						box.selected = true;
						box.styleName = partialStyleName;
						break;
					case SELECTED:
						box.selected = true;
						box.styleName = selectedStyleName;
						break;
				}
			}


			protected function box_changeHandler(event:Event):void {
				event.stopPropagation();
				if (box.selected) {
					selected = SELECTED;
				}
				else {
					selected = UNSELECTED;
				}
				dispatchEvent(new Event(Event.CHANGE));
			}

		]]>
	</mx:Script>
	<mx:CheckBox id="box" width="100%" height="100%" change="box_changeHandler(event)" />
</mx:VBox>
