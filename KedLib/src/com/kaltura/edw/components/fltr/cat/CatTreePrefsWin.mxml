<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:containers="com.kaltura.containers.*" 
							layout="vertical" width="450" height="190" showCloseButton="true"  
							styleName="TitleWindowType2" paddingTop="24" 
							creationComplete="creationCompleteHandler(event)" 
							title="{resourceManager.getString('filter', 'preferencesWindowTitle')}" >
	<mx:Script>
		<![CDATA[
			import com.kaltura.edw.components.fltr.cat.events.CatTreePrefsEvent;
			
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			
			
			private var _oldSelectionMode:int;
			public var selectionMode:int;
			
			[Bindable]
			private var _exactString:String;
			
			[Bindable]
			private var _plusString:String;
			
			public function setMode(object:String):void {
				if (object == "entries") {
					_exactString = resourceManager.getString('filter', 'preferencesExactEnt');
					_plusString = resourceManager.getString('filter', 'preferencesPlusEnt');
				}
				else {
					_exactString = resourceManager.getString('filter', 'preferencesExactCat');
					_plusString = resourceManager.getString('filter', 'preferencesPlusCat');
				}
			}
			
			/**
			 * save prefs and dispatch change event if needed, then request close.
			 * */
			protected function saveBtn_clickHandler(event:MouseEvent):void {
				if (selectionMode != _oldSelectionMode) {
					dispatchEvent(new CatTreePrefsEvent(CatTreePrefsEvent.PREFS_CHANGED, selectionMode));
				}
				// dispatch close event
				dispatchEvent(new CloseEvent(CloseEvent.CLOSE));
			}
			

			/**
			 * return current value
			 * */
			public function getValue():int {
				if (selectionModeGroup.selection == exact) {
					return CatTreeSelectionMode.MULTIPLE_SELECT_EXACT;
				}
				else {
					return CatTreeSelectionMode.MULTIPLE_SELECT_PLUS; 
				}
			}


			/**
			 * save old value and show it in radio buttons
			 * */
			protected function creationCompleteHandler(event:FlexEvent):void {
				if (selectionMode) {
					_oldSelectionMode = selectionMode;
				
					if (_oldSelectionMode == CatTreeSelectionMode.MULTIPLE_SELECT_EXACT) {
						exact.selected = true;
						plus.selected = false;
					}
					else {
						exact.selected = false;
						plus.selected = true;
					}
				}
				else {
					exact.selected = true;
					plus.selected = false;
				}
			}


			/**
			 * remember selected value
			 * */
			protected function selectionMode_changeHandler(event:Event):void
			{
				if (selectionModeGroup.selection == exact) {
					selectionMode = CatTreeSelectionMode.MULTIPLE_SELECT_EXACT;
				}
				else if (selectionModeGroup.selection == plus) {
					selectionMode = CatTreeSelectionMode.MULTIPLE_SELECT_PLUS;
				}
			}


		]]>
	</mx:Script>
	<mx:RadioButtonGroup id="selectionModeGroup" change="selectionMode_changeHandler(event)"/>
	<mx:RadioButton id="exact" group="{selectionModeGroup}" label="{_exactString}" />
	<mx:RadioButton id="plus" group="{selectionModeGroup}" label="{_plusString}" />
	<mx:ControlBar horizontalGap="5">
		<mx:Button id="saveBtn" label="{resourceManager.getString('filter','save')}" click="saveBtn_clickHandler(event)"/>
	</mx:ControlBar>
</mx:TitleWindow>
